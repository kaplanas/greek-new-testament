#################################
#### Top-level constructions ####
#################################

# Single-constituent sentences

S -> J[STANDALONE=y]
S -> L[STANDALONE=y]
S -> NP
S -> A
S -> AdjP
S -> IP

# Subject + predicate

IP -> VP[FINITE=y]
IP -> NP[WH=n, CASE=nominative, PERSON=?p, NUMBER=?n] VP[FINITE=y, PERSON=?p, NUMBER=?n]
IP -> VP[FINITE=y, PERSON=?p, NUMBER=?n] NP[WH=n, CASE=nominative, PERSON=?p, NUMBER=?n]

# Wh constructions

IP -> NP[WH=y, CASE=nominative, PERSON=3, NUMBER=?n] VP[FINITE=y, PERSON=3, NUMBER=?n]
IP -> AdjP[WH=y] IP

# Initial conjunction always allowed

S -> C[SECOND_POSITION=n] S

######################
#### Noun phrases ####
######################

# Noun phrases

NP[WH=?w, CASE=?c, PERSON=?p, NUMBER=?n] -> N_like[WH=?w, CASE=?c, PERSON=?p, NUMBER=?n]
NP[WH=n, CASE=?c, PERSON=3, NUMBER=?n] -> D[CASE=?c, NUMBER=?n] N_like[WH=n, CASE=?c, PERSON=3, NUMBER=?n]
NP[WH=n, CASE=?c, PERSON=3, NUMBER=?n] -> D[CASE=?c, NUMBER=?n] A[CASE=?c, NUMBER=?n]

# Noun-like vocabulary items

N_like[WH=n, CASE=?c, PERSON=3, NUMBER=?n] -> N[CASE=?c, NUMBER=?n]
N_like[WH=n, CASE=?c, PERSON=?p, NUMBER=?n] -> E[CASE=?c, PERSON=?p, NUMBER=?n]
N_like[WH=n, CASE=?c, PERSON=3, NUMBER=?n] -> M[CASE=?c, NUMBER=?n]
N_like[WH=?w, CASE=?c, PERSON=3, NUMBER=?n] -> I[CASE=?c, WH=?w, NUMBER=?n]
N_like[WH=n, CASE=?c, PERSON=3, NUMBER=?n] -> A[STANDALONE=y, CASE=?c, NUMBER=?n]

######################
#### Verb phrases ####
######################

# Ordinary verbs

VP[FINITE=?f, MOOD=?m, PERSON=?p, NUMBER=?n] -> V[FINITE=?f, MOOD=?m, PERSON=?p, NUMBER=?n]

# Copula verbs

PredP[WH=n, CASE=nominative, PERSON=3, NUMBER=?n] -> NP[WH=n, CASE=nominative, PERSON=3, NUMBER=?n]
PredP[WH=n, CASE=nominative, PERSON=3, NUMBER=?n] -> A[CASE=nominative, NUMBER=?n]

VP[FINITE=y, MOOD=indicative, PERSON=3, NUMBER=?n] -> PredP[WH=n, CASE=nominative, PERSON=3, NUMBER=?n]
VP[FINITE=y, MOOD=?m, PERSON=?p, NUMBER=?n] -> V[COPULA=y, FINITE=y, MOOD=?m, PERSON=?p, NUMBER=?n] PredP[WH=n, CASE=nominative, PERSON=3, NUMBER=?n]
VP[FINITE=y, MOOD=?m, PERSON=?p, NUMBER=?n] -> PredP[WH=n, CASE=nominative, PERSON=3, NUMBER=?n] V[COPULA=y, FINITE=y, MOOD=?m, PERSON=?p, NUMBER=?n]

# Negated verbs

V[FINITE=?f, MOOD=?m, PERSON=?p, NUMBER=?n] -> Neg V[FINITE=?f, MOOD=?m, PERSON=?p, NUMBER=?n]

#########################
#### Adjunct phrases ####
#########################

# Adverbs as adjuncts

AdjP[WH=?w] -> B[WH=?w]

#######################
#### Miscellaneous ####
#######################

# Negative particles

Neg -> B[NEGATIVE=y]
Neg -> L[NEGATIVE=y]

