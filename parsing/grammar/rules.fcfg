#################################
#### Top-level constructions ####
#################################

#### Basic sentence types ####

# Major constructions

S -> J[STANDALONE=y]
S -> L[STANDALONE=y]
S -> IP[SUBJECT=y]
S -> IP[SUBJECT=n, VERBLESS=n]
S -> C[SECOND_POSITION=n] C[SECOND_POSITION=y] Comma IP[DE=n]
S -> NP[FUSED_REL=n]
S -> NP[FUSED_REL=y, REL=y]
S -> Neg C[SECOND_POSITION=y] NP[FUSED_REL=n]
S -> AdvP
S -> C[SECOND_POSITION=n] C[SECOND_POSITION=y] AdvP[PREDICATE=n]
S -> VP[VERBLESS=n, FINITE=n, MOOD=infinitive]

# Minor constructions

S -> D[CASE=nominative] C[DE=y, SECOND_POSITION=y]
S -> D[CASE=nominative] C[DE=y, SECOND_POSITION=y] PP
S -> VP[FINITE=y, MOOD=imperative, NEGATIVE=n, VERBLESS=n] Comma[OPTIONAL=y] IP[DE=n, NEGATIVE=n, VERBLESS=n, WH=n]
S -> E[AGR=?a] IP[AGR=?a, SUBJECT=n, WH=y]
S -> NP[CASE=accusative] VP[CASE=accusative, MOOD=participle]
S -> AP PP
S -> 'ou)ai/_J-------' NP[DE=n, CASE=dative, FUSED_REL=n]
S -> 'o(/ra_V2PAM-S-' 'mh/_B-------'
S -> 'mara/na_N----VSM' 'qa/_V2AAM-S-'
S -> 'abba_N----VSM' 'o(_D----NSM' 'path/r_N----NSM'
S -> 'e)lwi/+_N----VSM' 'e)lwi/+_N----VSM' 'lema/_B-------' 'sabaxqa/ni_V2AAI-S-'
S -> 'nai/_L-------' ',_,' 'a)mh/n_J-------'
S -> 'ei)/te_C-------' IP Comma[OPTIONAL=n] NP[CASE=dative]

# Things that can get tacked onto sentences

S -> AdjP[COMMA_OPTIONAL=?co] Comma[OPTIONAL=?co] S
S -> S Comma[OPTIONAL=?co] AdjP[COMMA_OPTIONAL=?co]
S -> C[SECOND_POSITION=n] AdjP[COMMA_OPTIONAL=?co] Comma[OPTIONAL=?co] S

# Traces and other empty things

NP[AGR=?a, CASE=?c, POS=NP, PREDICATE=?pred, PROPER=?p, STANDALONE=?s, WH=?w]/NP[AGR=?a, CASE=?c, POS=NP, PREDICATE=?pred, PROPER=?p, STANDALONE=?s, WH=?w] ->
VP[AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=?p, VERBLESS=n]/VP[AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=?p, VERBLESS=n] ->
AP[AGR=?a, CASE=?c, POS=AP, WH=?w]/AP[AGR=?a, CASE=?c, POS=AP, WH=?w] ->
AdvP[DE=?d, POS=AdvP, PREDICATE=?p, WH=?w]/AdvP[DE=?d, POS=AdvP, PREDICATE=?p, WH=?w] ->
PredP[AGR=?a, CASE=?c, WH=?w]/PredP[AGR=?a, CASE=?c, WH=?w] ->
V[AGR=?a, ARGS=?arg, CASE=?c, FINITE=?f, MOOD=?m]/V[AGR=?a, ARGS=?arg, CASE=?c, FINITE=?f, MOOD=?m] ->
N[AGR=?a, CASE=?c, DE=n, POS=N, PROPER=?p, STANDALONE=?s, WH=?w]/N[AGR=?a, CASE=?c, DE=n, POS=N, PROPER=?p, STANDALONE=?s, WH=?w] ->
C[DE=n, SECOND_POSITION=y] ->

Comma -> ',_,'
Comma[OPTIONAL=y] ->

#### Finite constructions ####

# Predicate, no subject

IP[AGR=?a, DE=?d, MOOD=?m, REL=n, SUBJECT=n, VERBLESS=?v, WH=n] -> VP[AGR=?a, DE=?d, FINITE=y, MOOD=?m, VERBLESS=?v]
IP[AGR=?a, DE=?d, MOOD=?m, REL=n, SUBJECT=n, VERBLESS=?v, WH=n]/?x[CASE=?c, POS=NP] -> VP[AGR=?a, DE=?d, FINITE=y, MOOD=?m, VERBLESS=?v]/?x[CASE=?c, POS=NP]
IP[AGR=?a, DE=?d, MOOD=?m, REL=n, SUBJECT=n, VERBLESS=?v, WH=?w]/?x[AGR=?agr, ATTRIBUTIVE=?attr, CASE=?c, DE=?d, POS=AP, WH=?w] -> VP[AGR=?a, DE=n, FINITE=y, MOOD=?m, VERBLESS=?v]/?x[AGR=?agr, ATTRIBUTIVE=?attr, CASE=?c, DE=?d, POS=AP, WH=?w]
IP[AGR=?a, DE=?d, MOOD=?m, REL=n, SUBJECT=n, WH=y]/?x[POS=PredP, WH=y] -> VP[AGR=?a, DE=?d, FINITE=y, MOOD=?m, VERBLESS=n]/?x[POS=PredP, WH=y]

# Subject + predicate

IP[AGR=?a, DE=?d, MOOD=?m, REL=n, SUBJECT=y, VERBLESS=?v, WH=n] -> NP[AGR=?a, DE=?d, CASE=nominative, FUSED_REL=n, WH=n] VP[AGR=?a, FINITE=y, DE=n, MOOD=?m, VERBLESS=?v]
IP[AGR=?a, DE=?d, MOOD=?m, REL=n, SUBJECT=y, VERBLESS=?v, WH=n] -> NP[AGR=?a, DE=?d, CASE=nominative, FUSED_REL=n, WH=n] 'mh/_B-------' VP[AGR=?a, FINITE=y, DE=n, MOOD=?m, VERBLESS=?v]
IP[AGR=?a, DE=?d, MOOD=?m, REL=n, SUBJECT=y, VERBLESS=n, WH=n] -> NP[AGR=?a, DE=?d, CASE=nominative, FUSED_REL=y, WH=n] VP[AGR=?a, FINITE=y, DE=n, MOOD=?m, VERBLESS=n]
IP[AGR=?a, DE=?d, MOOD=?m, REL=n, SUBJECT=y, VERBLESS=?v, WH=n] -> C[SECOND_POSITION=n] NP[AGR=?a, DE=?d, CASE=nominative, FUSED_REL=n, WH=n] Comma[OPTIONAL=n] VP[AGR=?a, FINITE=y, DE=n, MOOD=?m, VERBLESS=?v]
IP[AGR=?a, DE=n, MOOD=?m, REL=n, SUBJECT=y, VERBLESS=?v, WH=n]/?x[CASE=?c, POS=NP] -> NP[AGR=?a, CASE=nominative, DE=n, WH=n] VP[AGR=?a, DE=n, FINITE=y, MOOD=?m, VERBLESS=?v]/?x[CASE=?c, POS=NP]
IP[AGR=?a, DE=n, MOOD=?m, REL=n, SUBJECT=y, VERBLESS=?v, WH=n]/?x[POS=AP] -> NP[AGR=?a, CASE=nominative, DE=n, WH=n] VP[AGR=?a, DE=n, FINITE=y, MOOD=?m, VERBLESS=?v]/?x[POS=AP]
IP[AGR=?a, DE=n, MOOD=?m, REL=n, SUBJECT=y, VERBLESS=?v, WH=?w]/?x[POS=PredP, WH=?w] -> NP[AGR=?a, CASE=nominative, DE=n, WH=n] VP[AGR=?a, DE=n, FINITE=y, MOOD=?m, VERBLESS=?v]/?x[POS=PredP, WH=?w]
IP[AGR=?a, DE=n, MOOD=?m, REL=n, SUBJECT=y, VERBLESS=?v, WH=n]/?x[POS=AdvP, WH=n] -> NP[AGR=?a, CASE=nominative, DE=n, WH=n] VP[AGR=?a, DE=n, FINITE=y, MOOD=?m, VERBLESS=?v]/?x[POS=AdvP, WH=n]
IP[AGR=?a, DE=n, MOOD=?m, REL=n, SUBJECT=y, VERBLESS=?v, WH=n]/?x[FINITE=n, POS=VP] -> NP[AGR=?a, CASE=nominative, DE=n, WH=n] VP[AGR=?a, DE=n, FINITE=y, MOOD=?m, VERBLESS=?v]/?x[FINITE=n, POS=VP]
IP[AGR=?a, DE=n, MOOD=?m, REL=n, SUBJECT=y, WH=n]/?x[ARGS=?arg, POS=V] -> NP[AGR=?a, CASE=nominative, DE=n, WH=n] VP[AGR=?a, DE=n, FINITE=y, MOOD=?m, VERB_ONLY=n, VERBLESS=n]/?x[ARGS=?arg, POS=V]

# Subject + predicate, idiosyncractic agreement

IP[AGR=[GENDER=neuter, NUMBER=plural, PERSON=3], DE=?d, MOOD=?m, REL=n, SUBJECT=y, VERBLESS=?v, WH=n] -> NP[AGR=[GENDER=neuter, NUMBER=plural, PERSON=3], CASE=nominative, DE=?d, FUSED_REL=n, WH=n] VP[AGR=[GENDER=neuter, NUMBER=singular, PERSON=3], DE=n, FINITE=y, MOOD=?m, VERBLESS=?v]
IP[AGR=[GENDER=?g, NUMBER=plural, PERSON=2], DE=?d, MOOD=?m, REL=n, SUBJECT=y, WH=n] -> I[AGR=[GENDER=?g, NUMBER=plural, PERSON=3], CASE=nominative, DE=n, FUSED_REL=n, WH=n] VP[AGR=[GENDER=?g, NUMBER=plural, PERSON=2], DE=?d, FINITE=y, MOOD=?m, VERBLESS=n]
IP[AGR=?a, DE=?d, MOOD=?m, REL=n, SUBJECT=y, WH=n]/?x[POS=PredP, WH=n] -> I[AGR=[GENDER=?g, NUMBER=plural, PERSON=3], CASE=nominative, DE=n, FUSED_REL=n, WH=n] VP[AGR=[GENDER=?g, NUMBER=plural, PERSON=2], DE=?d, FINITE=y, MOOD=?m, VERBLESS=n]/?x[POS=PredP, WH=n]

# Predicate + subject

IP[AGR=?a, DE=?d, MOOD=?m, REL=n, SUBJECT=y, VERBLESS=?v, WH=n] -> VP[AGR=?a, DE=?d, FINITE=y, MOOD=?m, VERBLESS=?v, WH=n] NP[AGR=?a, CASE=nominative, DE=n, FUSED_REL=n, WH=n]
IP[AGR=?a, DE=n, MOOD=?m, REL=n, SUBJECT=y, WH=y]/?x[CASE=?c, POS=NP, PREDICATE=?p, WH=y] -> VP[AGR=?a, DE=n, FINITE=y, MOOD=?m, VERBLESS=n, WH=n]/?x[CASE=?c, POS=NP, PREDICATE=?p, WH=y] NP[AGR=?a, CASE=nominative, DE=n, WH=n]
IP[AGR=?a, DE=n, MOOD=?m, REL=n, SUBJECT=y, WH=y]/?x[POS=PredP, WH=y] -> VP[AGR=?a, DE=n, FINITE=y, MOOD=?m, VERBLESS=n]/?x[POS=PredP, WH=y] NP[AGR=?a, CASE=nominative, DE=n, WH=n]
IP[AGR=?a, DE=n, MOOD=?m, REL=n, SUBJECT=y, WH=?w]/?x[POS=N] -> VP[AGR=?a, DE=n, FINITE=y, MOOD=?m, VERBLESS=n] NP[AGR=?a, CASE=nominative, DE=n, WH=?w]/?x[POS=N]

# Predicate + subject, idiosyncractic agreement

IP[AGR=[GENDER=?g, NUMBER=plural, PERSON=1], DE=?d, MOOD=?m, REL=n, SUBJECT=y, WH=n] -> VP[AGR=[GENDER=?g, NUMBER=plural, PERSON=1], DE=?d, FINITE=y, MOOD=?m, VERBLESS=n] AP[AGR=[GENDER=?g, NUMBER=plural, PERSON=3], CASE=nominative, DE=n, FUSED_REL=n, WH=n]

# Minor types

IP[AGR=?a, DE=?d, MOOD=?m, REL=n, SUBJECT=n, WH=n] -> D[AGR=?a, CASE=nominative] 'de/_C-------' VP[AGR=?a, DE=n, FINITE=y, MOOD=?m, VERBLESS=n]
IP[AGR=?a, DE=?d, MOOD=imperative, REL=n, SUBJECT=y, WH=n] -> V[AGR=?a, ARGS=[ARG_DAT_INF=y], MOOD=imperative] C[DE=?d, SECOND_POSITION=y] NP[CASE=dative, DE=n, WH=n] NP[AGR=?a, CASE=nominative, DE=n, WH=n] VP[DE=n, MOOD=infinitive]

#### IPs with movement ####

# Wh subject

IP[AGR=?a, DE=?d, MOOD=?m, REL=?r, SUBJECT=y, VERBLESS=?v, WH=y] -> NP[AGR=?a, CASE=nominative, DE=?d, REL=?r, WH=y] VP[AGR=?a, DE=n, FINITE=y, MOOD=?m, PREDICATE=n, VERBLESS=?v]
IP[AGR=?a, DE=?d, MOOD=?m, REL=?r, SUBJECT=y, VERBLESS=?v, WH=y] -> NP[AGR=?a[GENDER=neuter, NUMBER=plural, PERSON=3], CASE=nominative, DE=?d, REL=?r, WH=y] VP[AGR=[GENDER=neuter, NUMBER=singular, PERSON=3], DE=n, FINITE=y, MOOD=?m, VERBLESS=?v]

# Wh elements extracted out of an IP

IP[AGR=?a, DE=?d, MOOD=?m, REL=?r, SUBJECT=?s, VERBLESS=?v, WH=y] -> NP[AGR=?agr, DE=?d, CASE=?c, PREDICATE=?pred, PROPER=?p, REL=?r, STANDALONE=?sa, WH=y] IP[AGR=?a, DE=n, MOOD=?m, SUBJECT=?s, VERBLESS=?v]/NP[AGR=?agr, CASE=?c, POS=NP, PREDICATE=?pred, PROPER=?p, REL=?r, STANDALONE=?sa, WH=y]
IP[AGR=?a, DE=?d, MOOD=?m, REL=?r, SUBJECT=?s, WH=y] -> AdvP[DE=?d, PREDICATE=?p, REL=?r, WH=y] IP[AGR=?a, DE=n, MOOD=?m, SUBJECT=?s, VERBLESS=n]
IP[AGR=?a, DE=?d, MOOD=?m, REL=?r, SUBJECT=?s, WH=y] -> AdvP[DE=?d, PREDICATE=?p, REL=?r, WH=y] 'o(/ti_C-------' IP[AGR=?a, DE=n, MOOD=?m, SUBJECT=?s, VERBLESS=n]
IP[AGR=?a, DE=?d, MOOD=?m, REL=?r, SUBJECT=?s, WH=y] -> PredP[DE=?d, REL=?r, WH=y] IP[AGR=?a, DE=n, MOOD=?m, SUBJECT=?s, VERBLESS=?v]/PredP[POS=PredP, REL=?r, WH=y]

# Other things moved out of an IP

IP[AGR=?a, DE=?d, MOOD=?m, REL=n, SUBJECT=y, WH=n] -> NP[AGR=?agr, DE=?d, CASE=?c, PREDICATE=?pred, PROPER=?p, STANDALONE=?sa, WH=n] IP[AGR=?a, DE=n, MOOD=?m, SUBJECT=y, VERBLESS=n]/NP[AGR=?agr, CASE=?c, POS=NP, PREDICATE=?pred, PROPER=?p, STANDALONE=?sa, WH=n]
IP[AGR=?a, DE=?d, MOOD=?m, REL=n, SUBJECT=?s, VERBLESS=?v, WH=n] -> VP[AGR=?agr, CASE=?c, DE=?d, FINITE=?f, MOOD=?mood, POS=VP, PREDICATE=?p, VERBLESS=n] IP[AGR=?a, DE=n, MOOD=?m, SUBJECT=?s, VERBLESS=?v, WH=n]/VP[AGR=?agr, CASE=?c, FINITE=?f, MOOD=?mood, POS=VP, PREDICATE=?p, VERBLESS=n]
IP[AGR=?a, DE=?d, MOOD=?m, REL=n, SUBJECT=?s, VERBLESS=?v, WH=?w] -> AP[AGR=?agr, CASE=?c, DE=?d, WH=?w] IP[AGR=?a, DE=n, MOOD=?m, SUBJECT=?s, VERBLESS=?v]/AP[AGR=?agr, CASE=?c, POS=AP, WH=?w]
IP[AGR=?a, DE=?d, MOOD=?m, REL=n, SUBJECT=?s, VERBLESS=?v, WH=?w] -> AdvP[DE=?d, POS=AdvP, WH=?w] IP[AGR=?a, DE=n, MOOD=?m, SUBJECT=?s, VERBLESS=?v]/AdvP[POS=AdvP, WH=?w]
IP[AGR=?a, DE=?d, MOOD=?m, REL=n, SUBJECT=y, WH=?w] -> N[AGR=?agr, CASE=?c, DE=n, POS=N, PROPER=?p, STANDALONE=?s, WH=?w] C[DE=?d, SECOND_POSITION=y] IP[AGR=?a, DE=n, MOOD=?m, SUBJECT=y, VERBLESS=n, WH=?w]/N[AGR=?a, CASE=?c, DE=n, POS=N, PROPER=?p, STANDALONE=?s, WH=?w]
IP[AGR=?a, DE=?d, MOOD=?m, REL=n, SUBJECT=y] -> V[AGR=?a, ARGS=?arg, CASE=?c, FINITE=?f, MOOD=?m] C[DE=?d, SECOND_POSITION=y] IP[AGR=?a, DE=n, MOOD=?m, SUBJECT=y, VERBLESS=n]/V[AGR=?a, ARGS=?arg, CASE=?c, FINITE=?f, MOOD=?m]
IP[AGR=?a, DE=?d, MOOD=?m, REL=n, SUBJECT=y, WH=n] -> PredP[AGR=?agr, CASE=?c, DE=?d, WH=n] IP[AGR=?a, DE=n, MOOD=?m, SUBJECT=y, VERBLESS=n]/?x[AGR=?agr, CASE=?c, POS=PredP, WH=n]

# Adverbs in front of a wh element
IP[AGR=?a, DE=?d, MOOD=?m, REL=?r, SUBJECT=?s, VERBLESS=?v, WH=y] -> AdvP[DE=?d, WH=n] IP[AGR=?a, DE=n, MOOD=?m, REL=?r, SUBJECT=?s, VERBLESS=?v, WH=y]

#### Other things that can happen to IPs ####

# Negation

IP[AGR=?a, DE=?d, MOOD=?m, REL=n, SUBJECT=?s, VERBLESS=?v, WH=n] -> 'mh/_B-------' C[DE=?d, SECOND_POSITION=y] IP[AGR=?a, DE=n, MOOD=?m, SUBJECT=?s, VERBLESS=?v]
IP[AGR=?a, DE=?d, MOOD=?m, REL=n, SUBJECT=?s, VERBLESS=?v, WH=n] -> 'mh/ti_L-------' C[DE=?d, SECOND_POSITION=y] IP[AGR=?a, DE=n, MOOD=?m, SUBJECT=?s, VERBLESS=?v]
IP[AGR=?a, DE=?d, MOOD=?m, REL=n, SUBJECT=?s, VERBLESS=?v, WH=n] -> 'ou)xi/_L-------' C[DE=?d, SECOND_POSITION=y] IP[AGR=?a, DE=n, MOOD=?m, SUBJECT=?s, VERBLESS=?v]

# Conjunction

IP[AGR=?a, DE=?d, MOOD=?m, REL=n, SUBJECT=?s, VERBLESS=?v, WH=n] -> IP[AGR=?a, DE=?d, MOOD=?m, SUBJECT=?s, VERBLESS=?v, WH=n] Comma C[SECOND_POSITION=n] IP[DE=?d, VERBLESS=?v, WH=n]
IP[AGR=?a, DE=?d, MOOD=?m, REL=n, SUBJECT=?s, VERBLESS=?v, WH=n] -> C[DE=y, SUBORDINATING=y] C[DE=?d, SECOND_POSITION=y] IP[DE=?d, VERBLESS=?v, WH=n] IP[AGR=?a, DE=n, MOOD=?m, SUBJECT=?s, VERBLESS=?v, WH=n]

######################
#### Noun phrases ####
######################

#### Building NPs ####

# Noun-like vocabulary items

N_like[AGR=?a, ARGS=?arg, CASE=?c, DE=?d, DEGREE=absolute, PROPER=?p, REL=n, WH=n] -> N[AGR=?a, ARGS=?arg, CASE=?c, PROPER=?p] C[DE=?d, SECOND_POSITION=y]
N_like[AGR=?a, ARGS=none, CASE=?c, DE=?d, DEGREE=?deg, PAS=?pas, PROPER=?p, REL=n, STANDALONE=y, WH=?w] -> AP[AGR=?a, CASE=?c, DE=?d, DEGREE=?deg, KAI=n, PAS=?pas, PROPER=?p, STANDALONE=y, WH=?w]

# Single-constituent NPs

NP[AGR=?a, CASE=?c, DE=?d, FUSED_REL=n, POS=NP, PREDICATE=n, PROPER=n, REL=n, STANDALONE=n, WH=n] -> E[AGR=?a, CASE=?c] C[DE=?d, SECOND_POSITION=y]
NP[AGR=?a, CASE=?c, DE=?d, FUSED_REL=n, POS=NP, PREDICATE=y, PROPER=n, REL=n, STANDALONE=n, WH=n] -> M[AGR=?a, CASE=?c] C[DE=?d, SECOND_POSITION=y]
NP[AGR=?a, CASE=?c, DE=?d, FUSED_REL=n, POS=NP, PREDICATE=?w, PROPER=n, REL=n, STANDALONE=n, WH=?w] -> I[AGR=?a, CASE=?c, WH=?w] C[DE=?d, SECOND_POSITION=y]
NP[AGR=?a, CASE=?c, DE=?d, FUSED_REL=n, POS=NP, PREDICATE=n, PROPER=n, REL=y, STANDALONE=n, WH=y] -> R[AGR=?a, CASE=?c] C[DE=?d, SECOND_POSITION=y]

# NPs with (mostly) ordinary nouns and possibly other things

NP[AGR=[GENDER=?g, NUMBER=?n, PERSON=3], CASE=?c, DE=?d, FUSED_REL=n, POS=NP, PREDICATE=y, PROPER=?p, REL=?r, STANDALONE=?s, WH=?w] -> N_like[AGR=[GENDER=?g, NUMBER=?n, PERSON=3], CASE=?c, DE=?d, PROPER=?p, REL=?r, STANDALONE=?s, WH=?w]
NP[AGR=[GENDER=?g, NUMBER=?n, PERSON=3], CASE=?c, DE=?d, FUSED_REL=n, POS=NP, PREDICATE=y, PROPER=?p, REL=?r, STANDALONE=?s, WH=n] -> D[AGR=?a, CASE=?c] C[DE=?d, SECOND_POSITION=y] N_like[AGR=[GENDER=?g, NUMBER=?n, PERSON=3], CASE=?c, DE=n, PROPER=?p, REL=?r, STANDALONE=?s, WH=n]
NP[AGR=?a, CASE=?c, DE=?d, FUSED_REL=n, POS=NP, PREDICATE=y, PROPER=?p, REL=?r, STANDALONE=?s, WH=n] -> D[AGR=?a, CASE=?c] C[DE=?d, SECOND_POSITION=y] AP[AGR=?a, ATTRIBUTIVE=y, CASE=?c, DE=n, PAS=n, WH=n] N_like[AGR=?a[PERSON=3], CASE=?c, DE=n, PROPER=?p, REL=?r, STANDALONE=?s, WH=n]
NP[AGR=?a, CASE=?c, DE=?d, FUSED_REL=n, POS=NP, PREDICATE=y, PROPER=n, REL=n, STANDALONE=n, WH=n] -> D[AGR=?a, CASE=?c] C[DE=?d, SECOND_POSITION=y] M[AGR=?a, CASE=?c, WH=n]
NP[AGR=?a, CASE=?c, DE=?d, FUSED_REL=n, POS=NP, PREDICATE=y, PROPER=?p, REL=?r, STANDALONE=?s, WH=?w] -> AP[AGR=?a, ATTRIBUTIVE=y, CASE=?c, DE=?d, PAS=n, WH=n] N_like[AGR=?a, CASE=?c, DE=n, PAS=n, PROPER=?p, REL=?r, STANDALONE=?s, WH=?w]
NP[AGR=?a, CASE=?c, DE=?d, FUSED_REL=n, POS=NP, PREDICATE=y, PROPER=?p, REL=?r, STANDALONE=?s, WH=?w] -> N_like[AGR=?a, CASE=?c, DE=n, PAS=n, PROPER=?p, REL=?r, STANDALONE=?s, WH=?w] C[DE=?d, SECOND_POSITION=y] AP[AGR=?a, ATTRIBUTIVE=y, CASE=?c, DE=n, WH=n]
NP[AGR=?a, CASE=?c, DE=?d, FUSED_REL=n, POS=NP, PREDICATE=y, PROPER=n, REL=n, STANDALONE=?s, WH=n] -> D[AGR=?a, CASE=?c] C[DE=?d, SECOND_POSITION=y] VP[AGR=?a, CASE=?c, FINITE=n, MOOD=participle]
NP[AGR=?a, CASE=?c, DE=?d, FUSED_REL=n, POS=NP, PREDICATE=y, PROPER=?p, REL=?r, STANDALONE=?s, WH=y] -> AP[AGR=?a, ATTRIBUTIVE=y, CASE=?c, DE=?d, PAS=n, WH=y] N_like[AGR=?a, CASE=?c, DE=n, PAS=n, PROPER=?p, REL=?r, STANDALONE=?s, WH=n]
NP[AGR=?a[PERSON=3], CASE=?c, DE=?d, FUSED_REL=n, POS=NP, PREDICATE=?pred, PROPER=?p, REL=n, STANDALONE=n, WH=n] -> D[CASE=?c, AGR=?a] DE[SECOND_POSITION=y, DE=?d] AP[AGR=?a, CASE=?c, DE=n, PAS=n, PREDICATE=?pred, PROPER=?p]

# Fused relatives

NP[AGR=?a, CASE=nominative, DE=?d, FUSED_REL=y, POS=NP, PREDICATE=y, PROPER=?p, REL=?r, STANDALONE=n, WH=n] -> NP[AGR=?a, CASE=nominative, DE=?d, PROPER=?p, REL=?r, WH=y] VP[AGR=?a, DE=n, VERBLESS=n]
NP[AGR=?a, CASE=?c, DE=?d, FUSED_REL=y, POS=NP, PROPER=?p, REL=?r, STANDALONE=n, WH=n] -> NP[AGR=?a, CASE=?c, DE=?d, PROPER=?p, REL=?r, WH=y] IP[DE=n, VERBLESS=n]/NP[AGR=?a, CASE=?c, PROPER=?p, REL=?r, WH=y]
NP[CASE=oti, DE=?d, FUSED_REL=y, POS=NP, PROPER=n, REL=n, STANDALONE=n, WH=n] -> AdvP[DE=?d, WH=y] IP[DE=n, VERBLESS=n]

# to/ta + stuff

NP[AGR=?a, CASE=?c, DE=?d, FUSED_REL=n, POS=NP, PREDICATE=y, REL=n, STANDALONE=y, WH=n] -> D[AGR=?a[GENDER=neuter, NUMBER=plural], CASE=?c] C[DE=?d, SECOND_POSITION=y] PP
NP[AGR=?a, CASE=?c, DE=?d, FUSED_REL=n, POS=NP, PREDICATE=n, REL=n, STANDALONE=y, WH=n] -> D[AGR=?a[GENDER=neuter, NUMBER=singular], CASE=?c] C[DE=?d, SECOND_POSITION=y] VP[DE=n, MOOD=infinitive, VERBLESS=n]

# Movement out of NPs

NP[AGR=?a, CASE=?c, DE=?d, FUSED_REL=n, POS=NP, PROPER=?p, REL=?r, STANDALONE=?s, WH=?w]/?x[AGR=?a, ATTRIBUTIVE=?attr, CASE=?c, DE=?d, POS=AP, WH=?w] -> AP[AGR=?a, ATTRIBUTIVE=?attr, CASE=?c, DE=?d, WH=?w]/?x[POS=AP] N_like[AGR=?a, CASE=?c, DE=n, PROPER=?p, REL=?r, STANDALONE=?s, WH=n]
NP[AGR=?a, CASE=?c, DE=?d, FUSED_REL=n, POS=NP, PROPER=?p, REL=?r, STANDALONE=?s, WH=?w]/?x[AGR=?a, ATTRIBUTIVE=?attr, CASE=?c, DE=?d, POS=AP, WH=?w] -> AP[AGR=?a, ATTRIBUTIVE=?attr, CASE=?c, DE=?d, WH=?w]/?x[POS=AP] N_like[AGR=?a, CASE=?c, DE=n, PROPER=?p, REL=?r, STANDALONE=?s, WH=n] NP[CASE=genitive, DE=?d, WH=n]
NP[AGR=?a, CASE=?c, DE=n, FUSED_REL=n, POS=NP, PROPER=?p, REL=?r, STANDALONE=?s, WH=?w]/?x[AGR=?a, CASE=?c, DE=n, POS=N, PROPER=?p, STANDALONE=?s, WH=?w] -> N[AGR=?a, CASE=?c, DE=n, POS=N, PROPER=?p, STANDALONE=?s, WH=n]/?x[AGR=?a, CASE=?c, DE=n, POS=N, PROPER=?p, REL=?r, STANDALONE=?s, WH=?w] NP[CASE=genitive, DE=?d, WH=n]

# Conjunction

NP[AGR=[NUMBER=plural], CASE=?c, DE=?d, FUSED_REL=n, POS=NP, PREDICATE=?p, REL=?r, STANDALONE=?s, WH=n] -> NP[CASE=?c, DE=?d, PREDICATE=?p, REL=?r, STANDALONE=?s, WH=n] C[SECOND_POSITION=n] NP[CASE=?c, DE=n, PREDICATE=?p, REL=?r, STANDALONE=?s, WH=n]

#### Adding things to NPs ####

# Genitive adjuncts to NPs

NP[AGR=?a, CASE=?c, DE=?d, FUSED_REL=n, POS=NP, PREDICATE=y, PROPER=?p, REL=?r, STANDALONE=?s, WH=?w] -> N_like[AGR=?a, CASE=?c, DE=n, DEGREE=absolute, PROPER=?p, REL=?r, STANDALONE=?s, WH=?w] C[DE=?d, SECOND_POSITION=y] NP[CASE=genitive, WH=n]
NP[AGR=?a, CASE=?c, DE=?d, FUSED_REL=n, POS=NP, PREDICATE=y, PROPER=?p, REL=n, STANDALONE=?s, WH=n] -> NP[CASE=genitive, DE=?d, REL=n, WH=n] D[AGR=?a, CASE=?c] N_like[AGR=?a[PERSON=3], CASE=?c, DE=n, DEGREE=absolute, PROPER=?p, REL=n, STANDALONE=?s, WH=n]
NP[AGR=?a, CASE=?c, DE=?d, FUSED_REL=n, POS=NP, PREDICATE=y, PROPER=?p, REL=y, STANDALONE=?s, WH=n] -> NP[CASE=genitive, DE=?d, REL=y] D[AGR=?a, CASE=?c] N_like[AGR=?a[PERSON=3], CASE=?c, DE=n, DEGREE=absolute, PROPER=?p, REL=n, STANDALONE=?s, WH=n]
NP[AGR=?a, CASE=?c, DE=?d, FUSED_REL=n, POS=NP, PREDICATE=y, PROPER=?p, REL=y, STANDALONE=?s, WH=n] -> NP[CASE=genitive, DE=?d, REL=n, WH=n] D[AGR=?a, CASE=?c] N_like[AGR=?a[PERSON=3], CASE=?c, DE=n, DEGREE=absolute, PROPER=?p, REL=y, STANDALONE=?s]
NP[AGR=?a, CASE=?c, DE=?d, FUSED_REL=n, POS=NP, PREDICATE=y, PROPER=?p, REL=?r, STANDALONE=?s, WH=n] -> NP[CASE=genitive, DE=?d, WH=n] N_like[AGR=?a[PERSON=3], CASE=?c, DE=n, DEGREE=absolute, PROPER=?p, REL=?r, STANDALONE=?s, WH=n]
NP[AGR=?a, CASE=?c, DE=?d, FUSED_REL=n, POS=NP, PREDICATE=y, PROPER=?p, REL=?r, STANDALONE=?s, WH=n] -> D[AGR=?a, CASE=?c] C[DE=?d, SECOND_POSITION=y] N_like[AGR=?a[PERSON=3], CASE=?c, DE=n, DEGREE=absolute, PROPER=?p, REL=?r, STANDALONE=?s, WH=n] NP[CASE=genitive, WH=n]

# Relative clause adjuncts to NPs

NP[AGR=[GENDER=?g, NUMBER=?n], CASE=vocative, DE=?d, FUSED_REL=n, POS=NP, PREDICATE=y, PROPER=?p, REL=?r, STANDALONE=?s, WH=?w] -> N_like[AGR=[GENDER=?g, NUMBER=?n], CASE=vocative, DE=n, DEGREE=absolute, PROPER=?p, REL=?r, STANDALONE=?s, WH=?w] C[DE=?d, SECOND_POSITION=y] NP[AGR=[NUMBER=?n], CASE=nominative, REL=y]

# autos

NP[AGR=?a, CASE=?c, DE=?d, FUSED_REL=n, POS=NP, PREDICATE=?p, PROPER=?prop, REL=?r, STANDALONE=?s, WH=?w] -> NP[AGR=?a[GENDER=?g, NUMBER=?n], CASE=?c, DE=?d, PREDICATE=?p, PROPER=?prop, REL=?r, STANDALONE=?s, WH=?w] E[AGR=[GENDER=?g, NUMBER=?n], AUTOS=y, CASE=?c]

# Dative complements to NPs

NP[AGR=?a, CASE=?c, DE=?d, FUSED_REL=n, POS=NP, PROPER=?p, REL=?r, STANDALONE=?s, WH=n] -> N_like[AGR=?a, ARGS=[ARG_DAT=y], DE=?d, CASE=?c, PROPER=?p, REL=?r, STANDALONE=?s, WH=n] NP[CASE=dative]
NP[AGR=?a, CASE=?c, DE=?d, FUSED_REL=n, POS=NP, PROPER=?p, REL=?r, STANDALONE=?s, WH=n] -> NP[CASE=dative, DE=?d] N_like[AGR=?a, ARGS=[ARG_DAT=y], DE=n, CASE=?c, PROPER=?p, REL=?r, STANDALONE=?s, WH=n]

# Pre-NP particles

NP[AGR=?a, CASE=?c, DE=?d, FUSED_REL=n, POS=NP, PROPER=?p, REL=?r, STANDALONE=?s, WH=n] -> 'kai/_B-------' NP[AGR=?a, CASE=?c, DE=?d, FUSED_REL=n, PROPER=?p, REL=?r, STANDALONE=?s, WH=n]
NP[AGR=?a, CASE=?c, DE=?d, FUSED_REL=n, POS=NP, PROPER=?p, REL=?r, STANDALONE=?s, WH=n] -> 'ou)de/_B-------' NP[AGR=?a, CASE=?c, DE=?d, FUSED_REL=n, PROPER=?p, REL=?r, STANDALONE=?s, WH=n]
NP[AGR=?a, CASE=?c, DE=?d, FUSED_REL=n, POS=NP, PROPER=?p, REL=?r, STANDALONE=?s, WH=n] -> 'ou)_B-------' NP[AGR=?a, CASE=?c, DE=?d, FUSED_REL=n, PROPER=?p, REL=?r, STANDALONE=?s, WH=n]

# Minor constructions

NP[AGR=?a, CASE=?c, DE=n, FUSED_REL=n, POS=NP, PROPER=y, REL=?r, STANDALONE=n, WH=n] -> N[AGR=?a, CASE=?c, PROPER=y] NP[AGR=?a, CASE=?c, DE=n, REL=?r, WH=n]
NP[AGR=?a, CASE=?c, DE=n, FUSED_REL=n, POS=NP, PROPER=y, REL=?r, STANDALONE=n, WH=n] -> NP[AGR=?a, CASE=?c, DE=n, PROPER=n, REL=?r, WH=n] N[AGR=?a, CASE=?c, PROPER=y]
NP[AGR=?a, CASE=?c, DE=n, FUSED_REL=n, POS=NP, PROPER=n, REL=n, STANDALONE=n, WH=n] -> I[AGR=?a, CASE=?c, WH=n] AP[AGR=?a, CASE=?c, DE=n, KAI=n, STANDALONE=n, WH=n]
NP[AGR=?a, CASE=?c, DE=?d, FUSED_REL=n, POS=NP, PREDICATE=n, PROPER=n, REL=n, STANDALONE=n, WH=n] -> M[AGR=?a, CASE=?c] C[DE=?d, SECOND_POSITION=y] A[AGR=?a, CASE=?c, PAS=y]
NP[AGR=?a, CASE=?c, DE=n, FUSED_REL=n, POS=NP, PREDICATE=?p, PROPER=n, REL=?r, STANDALONE=?s, WH=n] -> A[AGR=?a, CASE=?c, PAS=y] NP[AGR=?a, CASE=?c, DE=n, PREDICATE=?p, PROPER=n, REL=?r, STANDALONE=?s, WH=n]
NP[CASE=accusative, DE=?d, FUSED_REL=n, POS=NP, PROPER=n, REL=n, STANDALONE=n, WH=n] -> 'tinw=n_I----GPM'

#### Adjective phrases ####

# Bare adjectives

AP[AGR=?agr, ADVERB=?a, ATTRIBUTIVE=?att, CASE=?c, DE=?d, DEGREE=?deg, KAI=n, PAS=?pas, POS=AP, PREDICATE=?p, STANDALONE=?s, WH=?w] -> A[AGR=?agr, ADVERB=?a, ATTRIBUTIVE=?att, CASE=?c, DEGREE=?deg, PAS=?pas, PREDICATE=?p, STANDALONE=?s, WH=?w] C[DE=?d, SECOND_POSITION=y]

# Adjectives with arguments

AP[AGR=?agr, ADVERB=?a, ATTRIBUTIVE=?att, CASE=?c, DE=?d, DEGREE=?deg, KAI=n, PAS=?pas, POS=AP, PREDICATE=?p, STANDALONE=?s, WH=?w] -> NP[CASE=genitive, DE=?d] A[AGR=?agr, ADVERB=?a, ARGS=[ARG_GEN=y], ATTRIBUTIVE=?att, CASE=?c, DE=n, DEGREE=?deg, PAS=?pas, PREDICATE=?p, STANDALONE=?s, WH=?w]
AP[AGR=?agr, ADVERB=?a, ATTRIBUTIVE=?att, CASE=?c, DE=?d, DEGREE=?deg, KAI=n, PAS=?pas, POS=AP, PREDICATE=?p, STANDALONE=?s, WH=?w] -> A[AGR=?agr, ADVERB=?a, ARGS=[ARG_DAT=y], ATTRIBUTIVE=?att, CASE=?c, DEGREE=?deg, PAS=?pas, PREDICATE=?p, STANDALONE=?s, WH=?w] C[DE=?d, SECOND_POSITION=y] NP[CASE=dative, DE=?d]
AP[AGR=?agr, ADVERB=?a, ATTRIBUTIVE=?att, CASE=?c, DE=?d, DEGREE=comparative, KAI=n, PAS=?pas, POS=AP, PREDICATE=?p, STANDALONE=?s, WH=?w] -> A[AGR=?agr, ADVERB=?a, ATTRIBUTIVE=?att, CASE=?c, DE=?d, DEGREE=comparative, PAS=?pas, PREDICATE=?p, STANDALONE=?s, WH=?w] C[DE=?d, SECOND_POSITION=y] NP[CASE=genitive]

# Pre-AP particles

AP[AGR=?agr, ADVERB=?a, ATTRIBUTIVE=?att, CASE=?c, DE=?d, DEGREE=?deg, KAI=y, PAS=?pas, POS=AP, PREDICATE=?p, STANDALONE=?s, WH=n] -> 'kai/_B-------' AP[AGR=?agr, ADVERB=?a, ATTRIBUTIVE=?att, CASE=?c, DE=?d, DEGREE=?deg, KAI=n, PAS=?pas, PREDICATE=?p, STANDALONE=?s, WH=n]

######################
#### Verb phrases ####
######################

#### Ordinary verbs ####

# Intransitive verbs

VP[AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=n, VERB_ONLY=y, WH=n] -> V[AGR=?a, ARGS=[ARG_NONE=y], CASE=?c, FINITE=?f, MOOD=?m] C[DE=?d, SECOND_POSITION=y]
VP[AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=n, VERB_ONLY=y, WH=n]/?x[POS=V] -> V[AGR=?a, ARGS=[ARG_NONE=y], CASE=?c, FINITE=?f, MOOD=?m]/?x[POS=V] C[DE=?d, SECOND_POSITION=y]

# Accusative argument

VP[AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=n, WH=n] -> V[AGR=?a, ARGS=[ARG_ACC=y], CASE=?c, FINITE=?f, MOOD=?m] C[DE=?d, SECOND_POSITION=y] NP[CASE=accusative, DE=n, WH=n]
VP[AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=n, WH=n] -> NP[CASE=accusative, DE=n, WH=n] C[DE=?d, SECOND_POSITION=y] V[AGR=?a, ARGS=[ARG_ACC=y], CASE=?c, FINITE=?f, MOOD=?m]
VP[AGR=?a, CASE=?c, DE=n, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=n, WH=n]/?x[CASE=accusative, POS=NP] -> V[AGR=?a, ARGS=[ARG_ACC=y], CASE=?c, FINITE=?f, MOOD=?m] NP[CASE=accusative]/?x[CASE=accusative, POS=NP]
VP[AGR=?a, CASE=?c, DE=n, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=n, WH=?w]/?x[AGR=?agr, ATTRIBUTIVE=?attr, CASE=?c, DE=?d, POS=AP, WH=?w] -> V[AGR=?a, ARGS=[ARG_ACC=y], CASE=?c, FINITE=?f, MOOD=?m] NP[CASE=accusative]/?x[AGR=?agr, ATTRIBUTIVE=?attr, CASE=?c, DE=?d, POS=AP, WH=?w]
VP[AGR=?a, CASE=?c, DE=n, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=n, WH=n]/?x[POS=V] -> V[AGR=?a, ARGS=[ARG_ACC=y], CASE=?c, FINITE=?f, MOOD=?m]/?x[POS=V] NP[CASE=accusative, DE=n, WH=n]

# Dative argument

VP[AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=n, WH=n] -> V[AGR=?a, ARGS=[ARG_DAT=y], CASE=?c, FINITE=?f, MOOD=?m] C[DE=?d, SECOND_POSITION=y] NP[CASE=dative, DE=n, WH=n]
VP[AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=n, WH=n] -> NP[CASE=dative, DE=n, WH=n] C[DE=?d, SECOND_POSITION=y] V[AGR=?a, ARGS=[ARG_DAT=y], CASE=?c, FINITE=?f, MOOD=?m]
VP[AGR=?a, CASE=?c, DE=n, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=n, WH=n]/?x[ARGS=[ARG_DAT=y], POS=V] -> NP[CASE=dative, DE=n, WH=n] V[AGR=?a, ARGS=[ARG_DAT=y], CASE=?c, FINITE=?f, MOOD=?m]/?x[ARGS=?arg, POS=V]
VP[AGR=?a, CASE=?c, DE=n, FINITE=?f, MOOD=?m, PREDICATE=n, WH=n]/?x[CASE=dative, POS=NP] -> V[AGR=?a, ARGS=[ARG_DAT=y], CASE=?c, FINITE=?f, MOOD=?m] NP[CASE=dative]/?x[CASE=dative, POS=NP]

# Genitive argument

VP[AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=n, WH=n] -> NP[CASE=genitive, DE=?d, WH=n] V[AGR=?a, ARGS=[ARG_GEN=y], CASE=?c, FINITE=?f, MOOD=?m]
VP[AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=n, WH=n] -> V[AGR=?a, ARGS=[ARG_GEN=y], CASE=?c, FINITE=?f, MOOD=?m] C[DE=?d, SECOND_POSITION=y] NP[CASE=genitive, DE=n, WH=n]
VP[AGR=?a, CASE=?c, DE=n, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=n, WH=n]/?x[ARGS=[ARG_GEN=y], POS=V] -> NP[CASE=genitive, DE=n, WH=n] V[AGR=?a, ARGS=[ARG_GEN=y], CASE=?c, FINITE=?f, MOOD=?m]/?x[ARGS=?arg, POS=V]

# Infinitive argument

VP[AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=n, WH=n] -> VP[DE=?d, MOOD=infinitive, VERBLESS=n] V[AGR=?a, ARGS=[ARG_INF=y], CASE=?c, FINITE=?f, MOOD=?m]
VP[AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=n, WH=n] -> V[AGR=?a, ARGS=[ARG_INF=y], CASE=?c, FINITE=?f, MOOD=?m] C[DE=?d, SECOND_POSITION=y] VP[DE=n, MOOD=infinitive, VERBLESS=n]
VP[AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=n, WH=n]/?x[CASE=?c, POS=NP] -> V[AGR=?a, ARGS=[ARG_INF=y], CASE=?c, FINITE=?f, MOOD=?m] C[DE=?d, SECOND_POSITION=y] VP[DE=n, MOOD=infinitive, VERBLESS=n]/?x[CASE=?c, POS=NP]
VP[AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=n, WH=n] -> NP[CASE=?c, DE=?d, POS=NP, WH=n] V[AGR=?a, ARGS=[ARG_INF=y], CASE=?c, FINITE=?f, MOOD=?m] VP[DE=n, MOOD=infinitive, VERBLESS=n]/NP[CASE=?c, POS=NP, WH=n]

# Complement with oti

VP[AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=n, WH=n] -> V[AGR=?a, ARGS=[ARG_OTI=y], CASE=?c, FINITE=?f, MOOD=?m] C[DE=?d, SECOND_POSITION=y] 'o(/ti_C-------' VP[DE=n, FINITE=y, VERBLESS=n]
VP[AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=n, WH=n] -> V[AGR=?a, ARGS=[ARG_OTI=y], CASE=?c, FINITE=?f, MOOD=?m] C[DE=?d, SECOND_POSITION=y] NP[CASE=oti, DE=n, FUSED_REL=y]

# Full sentential argument

VP[AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=n, WH=n] -> V[AGR=?a, ARGS=[ARG_S=y], CASE=?c, FINITE=?f, MOOD=?m] C[DE=?d, SECOND_POSITION=y] Comma[OPTIONAL=n] S

# Subjunctive argument

VP[AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=n, WH=n] -> V[AGR=?a, ARGS=[ARG_SUBJ=y], CASE=?c, FINITE=?f, MOOD=?m] C[DE=?d, SECOND_POSITION=y] VP[DE=n, MOOD=subjunctive, VERBLESS=n]
VP[AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=n, WH=n]/?x[CASE=?c, POS=NP] -> V[AGR=?a, ARGS=[ARG_SUBJ=y], CASE=?c, FINITE=?f, MOOD=?m] C[DE=?d, SECOND_POSITION=y] VP[DE=n, MOOD=subjunctive, VERBLESS=n]/?x[CASE=?c, POS=NP]
VP[AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=n, WH=n] -> NP[CASE=?c, DE=?d, POS=NP, WH=n] V[AGR=?a, ARGS=[ARG_SUBJ=y], CASE=?c, FINITE=?f, MOOD=?m] VP[DE=n, MOOD=subjunctive, VERBLESS=n]/NP[CASE=?c, POS=NP, WH=n]

# Two accusative arguments

VP[AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=n, WH=n] -> NP[CASE=accusative, DE=?d, WH=n] PredP[CASE=accusative, DE=n, WH=n] V[AGR=?a, ARGS=[ARG_ACC_ACC=y], CASE=?c, FINITE=?f, MOOD=?m]
VP[AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=n, WH=n] -> NP[CASE=accusative, DE=?d, WH=n] V[AGR=?a, ARGS=[ARG_ACC_ACC=y], CASE=?c, FINITE=?f, MOOD=?m] PredP[CASE=accusative, DE=n, WH=n]
#VP[FINITE=?f, POS=VP, PREDICATE=n, CASE=?c, DE=?d, MOOD=?m, AGR=?a, WH=n] -> NP[WH=n, DE=?d, CASE=accusative] NP[WH=n, DE=n, CASE=accusative] V[CASE=?c, FINITE=?f, MOOD=?m, AGR=?a, ARGS=[ARG_ACC_ACC=y]]
VP[AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=n, WH=n] -> PredP[WH=n, DE=n, CASE=accusative] V[CASE=?c, FINITE=?f, MOOD=?m, AGR=?a, ARGS=[ARG_ACC_ACC=y]] NP[WH=n, DE=n, CASE=accusative]
VP[AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=n, WH=n]/?x[POS=PredP] -> NP[CASE=accusative, DE=?d, WH=n] V[AGR=?a, ARGS=[ARG_ACC_ACC=y], CASE=?c, FINITE=?f, MOOD=?m] PredP[CASE=accusative]/?x[POS=PredP]
VP[AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=n, WH=n]/?x[CASE=accusative, POS=NP] -> V[AGR=?a, ARGS=[ARG_ACC_ACC=y], CASE=?c, FINITE=?f, MOOD=?m] PredP[CASE=accusative, DE=n, WH=n] NP[CASE=accusative]/?x[CASE=accusative, POS=NP]

# Accusative and dative arguments

VP[AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=n, WH=n] -> V[AGR=?a, ARGS=[ARG_ACC_DAT=y], CASE=?c, FINITE=?f, MOOD=?m] C[DE=?d, SECOND_POSITION=y] NP[CASE=dative, DE=n, WH=n] NP[CASE=accusative, DE=n, WH=n]
VP[AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=n, WH=n] -> V[AGR=?a, ARGS=[ARG_ACC_DAT=y], CASE=?c, FINITE=?f, MOOD=?m] C[DE=?d, SECOND_POSITION=y] NP[CASE=accusative, DE=n, WH=n] NP[CASE=dative, DE=n, WH=n]
VP[AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=n, WH=n] -> NP[CASE=accusative, DE=?d, WH=n] NP[CASE=dative, DE=n, WH=n] V[AGR=?a, ARGS=[ARG_ACC_DAT=y], CASE=?c, FINITE=?f, MOOD=?m]
VP[AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=n, WH=n] -> NP[CASE=dative, DE=?d, WH=n] NP[CASE=accusative, DE=n, WH=n] V[AGR=?a, ARGS=[ARG_ACC_DAT=y], CASE=?c, FINITE=?f, MOOD=?m]
VP[AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=n, WH=n] -> NP[CASE=accusative, DE=?d, WH=n] V[AGR=?a, ARGS=[ARG_ACC_DAT=y], CASE=?c, FINITE=?f, MOOD=?m] NP[CASE=dative, DE=n, WH=n]
VP[AGR=?a, CASE=?c, DE=n, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=n, WH=n]/?x[CASE=accusative, POS=NP] -> V[AGR=?a, ARGS=[ARG_ACC_DAT=y], CASE=?c, FINITE=?f, MOOD=?m] NP[CASE=dative, DE=n, WH=n] NP[CASE=accusative]/?x[CASE=accusative, POS=NP]
VP[AGR=?a, CASE=?c, DE=n, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=n, WH=n]/?x[CASE=accusative, POS=NP] -> NP[CASE=dative, DE=n, WH=n] V[AGR=?a, ARGS=[ARG_ACC_DAT=y], CASE=?c, FINITE=?f, MOOD=?m] NP[CASE=accusative]/?x[CASE=accusative, POS=NP]

# Accusative and genitive arguments

VP[AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=n, WH=n] -> V[AGR=?a, ARGS=[ARG_ACC_GEN=y], CASE=?c, FINITE=?f, MOOD=?m] NP[CASE=accusative, DE=n, WH=n] NP[CASE=genitive, DE=n, WH=n]
VP[AGR=?a, CASE=?c, DE=n, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=n, WH=n]/?x[CASE=accusative, POS=NP] -> V[AGR=?a, ARGS=[ARG_ACC_GEN=y], CASE=?c, FINITE=?f, MOOD=?m] NP[CASE=genitive, DE=n, WH=n] NP[CASE=accusative]/?x[CASE=accusative, POS=NP]
VP[AGR=?a, CASE=?c, DE=n, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=n, WH=n]/?x[CASE=accusative, POS=NP] -> NP[CASE=genitive, DE=n, WH=n] V[AGR=?a, ARGS=[ARG_ACC_GEN=y], CASE=?c, FINITE=?f, MOOD=?m] NP[CASE=accusative]/?x[CASE=accusative, POS=NP]

# Accusative and infinitive arguments

VP[AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=n, WH=n] -> V[AGR=?a, ARGS=[ARG_ACC_INF=y], CASE=?c, FINITE=?f, MOOD=?m] C[SECOND_POSITION=y, DE=?d] NP[CASE=accusative, WH=n] VP[DE=n, MOOD=infinitive, VERBLESS=n]
VP[AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=n, WH=n] -> NP[CASE=accusative, DE=?d, WH=n] V[AGR=?a, ARGS=[ARG_ACC_INF=y], CASE=?c, FINITE=?f, MOOD=?m] VP[DE=n, MOOD=infinitive, VERBLESS=n]

# Dative and genitive arguments

VP[AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=n, WH=n] -> NP[CASE=genitive, DE=?d, WH=n] V[AGR=?a, ARGS=[ARG_DAT_GEN=y], CASE=?c, FINITE=?f, MOOD=?m] NP[CASE=dative, DE=n, WH=n]

# Dative and infinitive arguments

VP[AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=n, WH=n] -> V[AGR=?a, ARGS=[ARG_DAT_INF=y], CASE=?c, FINITE=?f, MOOD=?m] C[DE=?d, SECOND_POSITION=y] NP[CASE=dative, WH=n] VP[DE=n, MOOD=infinitive, VERBLESS=n]

#### Copula verbs ####

# Building predicates

PredP[CASE=?c, DE=?d, POS=PredP, WH=?w] -> NP[CASE=?c, DE=?d, FUSED_REL=n, PREDICATE=y, STANDALONE=n, WH=?w]
PredP[CASE=dative, DE=?d, POS=PredP, WH=?w] -> NP[CASE=dative, DE=?d, FUSED_REL=n, PREDICATE=n, STANDALONE=n, WH=?w]
PredP[AGR=[GENDER=?g, NUMBER=?n], CASE=?c, DE=?d, POS=PredP, WH=n] -> AP[AGR=[GENDER=?g, NUMBER=?n], CASE=?c, DE=?d, PREDICATE=y]
PredP[CASE=none, DE=?d, POS=PredP, WH=?w] -> AdvP[DE=?d, PREDICATE=y, WH=?w]
PredP[AGR=[GENDER=?g, NUMBER=?n], CASE=?c, DE=?d, POS=PredP, WH=n] -> VP[AGR=[GENDER=?g, NUMBER=?n], CASE=?c, DE=?d, FINITE=n, MOOD=participle, VERBLESS=n]
PredP[AGR=[GENDER=neuter, NUMBER=singular], CASE=?c, DE=?d, POS=PredP, WH=n] -> VP[AGR=[GENDER=neuter, NUMBER=plural], CASE=?c, DE=?d, FINITE=n, MOOD=participle, VERBLESS=n]
PredP[CASE=nominative, DE=?d, POS=PredP, WH=n] -> VP[DE=?d, FINITE=n, MOOD=infinitive, VERBLESS=n]

# Anything can be "like"

PredP[AGR=?a, CASE=?c, DE=?d, POS=PredP, WH=?w] -> 'w(s_P-------' PredP[AGR=?a, CASE=?c, DE=?d, WH=?w]

# Moving things out of predicates

PredP[CASE=none, DE=?d, POS=PredP, WH=n]/?x[POS=AdvP, WH=n] -> AdvP[DE=?d, PREDICATE=y, WH=n]/?x[POS=AdvP, WH=n]
PredP[CASE=genitive, DE=?d, POS=PredP, WH=?w]/?x[CASE=genitive, POS=NP] -> NP[CASE=genitive, DE=?d, FUSED_REL=n, PREDICATE=y, STANDALONE=n, WH=?w]/?x[CASE=genitive, POS=NP]
PredP[CASE=?c, DE=?d, POS=PredP, WH=?w]/?x[AGR=?a, CASE=?c, POS=AP, WH=?w] -> NP[CASE=?c, DE=?d, FUSED_REL=n, STANDALONE=n, WH=?w]/?x[AGR=?a, CASE=?c, POS=AP, WH=?w]

# Copula-predicate constructions

VP[AGR=?a, DE=?d, FINITE=y, MOOD=indicative, POS=VP, PREDICATE=y, VERBLESS=y, WH=n] -> PredP[AGR=?a, CASE=nominative, DE=?d, WH=n]
VP[DE=?d, FINITE=y, MOOD=indicative, POS=VP, PREDICATE=y, VERBLESS=y, WH=n] -> PredP[CASE=genitive, DE=?d, WH=n]
VP[DE=?d, FINITE=y, MOOD=indicative, POS=VP, PREDICATE=y, VERBLESS=y, WH=n] -> PredP[CASE=dative, DE=?d, WH=n]
VP[DE=?d, FINITE=y, MOOD=indicative, POS=VP, PREDICATE=y, VERBLESS=y, WH=?w] -> PredP[CASE=none, DE=?d, WH=?w]
VP[AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=y, WH=n] -> V[AGR=?a, CASE=?c, COPULA=y, FINITE=?f, MOOD=?m] C[DE=?d, SECOND_POSITION=y] PredP[AGR=?a, CASE=nominative, DE=n, WH=n]
VP[AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=y, WH=n] -> PredP[AGR=?a, CASE=nominative, DE=?d, WH=n] C[DE=?d, SECOND_POSITION=y] V[AGR=?a, CASE=?c, COPULA=y, FINITE=?f, MOOD=?m]

# Moving things out of copula-predicate constructions

VP[AGR=?a, CASE=?c, COPULA=y, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=y, WH=n]/?x[CASE=?c, POS=NP, PREDICATE=?p] -> V[AGR=?a, CASE=?c, COPULA=y, FINITE=?f, MOOD=?m, PREDICATE=y] PredP[CASE=nominative, DE=?d]/?x[CASE=?c, POS=NP, PREDICATE=?p]
VP[DE=?d, FINITE=y, MOOD=indicative, POS=VP, PREDICATE=y, VERBLESS=y, WH=n]/?x[CASE=?c, POS=NP] -> PredP[CASE=nominative, DE=?d]/?x[CASE=?c, POS=NP]
VP[CASE=?c, DE=n, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=y, WH=n]/?x[POS=AP] -> V[CASE=?c, COPULA=y, FINITE=?f, MOOD=?m] PredP[CASE=nominative, DE=n]/?x[POS=AP]
VP[DE=?d, FINITE=y, MOOD=indicative, POS=VP, PREDICATE=y, VERBLESS=y, WH=n]/?x[POS=AdvP, WH=n] -> PredP[CASE=none, DE=?d]/?x[POS=AdvP, WH=n]
VP[AGR=?a, CASE=nominative, DE=n, FINITE=y, MOOD=indicative, POS=VP, PREDICATE=y, VERBLESS=y, WH=n]/?x[POS=AP] -> PredP[AGR=?a, CASE=nominative]/?x[POS=AP]
VP[AGR=?a, CASE=?c, DE=n, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=y, VERBLESS=y, WH=y]/?x[POS=PredP, WH=y] -> PredP[AGR=?a, CASE=nominative]/?x[POS=PredP, WH=y]
VP[AGR=?a, CASE=?c, DE=n, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=y, WH=?w]/?x[POS=PredP, WH=?w] -> V[AGR=?a, CASE=?c, COPULA=y, FINITE=?f, MOOD=?m] PredP[AGR=?a, CASE=nominative]/?x[POS=PredP, WH=?w]

#### Things that get added to verbs and VPs ####

# Pre-verb particles

V[AGR=?a, ARGS=?args, CASE=?c, COPULA=?cop, FINITE=?f, MOOD=?m, NEGATIVE=y] -> 'ou)_B-------' V[AGR=?a, ARGS=?args, CASE=?c, COPULA=?cop, FINITE=?f, MOOD=?m, NEGATIVE=n]
V[AGR=?a, ARGS=?args, CASE=?c, COPULA=?cop, FINITE=?f, MOOD=?m, NEGATIVE=?n] -> 'kai/_B-------' V[AGR=?a, ARGS=?args, CASE=?c, COPULA=?cop, FINITE=?f, MOOD=?m, NEGATIVE=?n]

# Adjuncts

VP[ADVP=y, AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=?p, WH=n] -> AdvP[DE=?d, WH=n] VP[AGR=?a, CASE=?c, DE=n, FINITE=?f, MOOD=?m, PREDICATE=?p, VERBLESS=n]
VP[ADVP=y, AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=?p, WH=n]/?x[CASE=?c, POS=NP] -> VP[AGR=?a, CASE=?c, DE=n, FINITE=?f, MOOD=?m, PREDICATE=?p, VERBLESS=n]/?x[CASE=?c, POS=NP] AdvP[DE=?d, WH=n]
VP[ADVP=y, AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=?p, WH=n]/?x[CASE=?c, POS=NP] -> AdvP[DE=?d, WH=n] VP[AGR=?a, CASE=?c, DE=n, FINITE=?f, MOOD=?m, PREDICATE=?p, VERBLESS=n]/?x[CASE=?c, POS=NP]
VP[ADVP=y, AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=?p, WH=n] -> VP[AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, PREDICATE=?p, VERBLESS=n] AdvP[DE=n, WH=n]
VP[ADVP=y, AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=?p, WH=n] -> VP[AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, PREDICATE=?p, VERBLESS=n] AdvP[DE=n, REL=y]
VP[ADVP=y, AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=?p, WH=n]/?x[POS=V] -> AdvP[DE=?d, WH=n] VP[AGR=?a, CASE=?c, DE=n, FINITE=?f, MOOD=?m, PREDICATE=?p, VERBLESS=n]/?x[POS=V]
VP[ADVP=y, AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=?p, WH=n]/?x[POS=V] -> VP[AGR=?a, CASE=?c, DE=n, FINITE=?f, MOOD=?m, PREDICATE=?p, VERB_ONLY=n, VERBLESS=n]/?x[POS=V] AdvP[DE=?d, WH=n]
VP[ADVP=y, AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=?p, WH=n] -> V[AGR=?a, ARGS=?arg, CASE=?c, FINITE=?f, MOOD=?m] C[DE=?d, SECOND_POSITION=y] AdvP[DE=?d, WH=n] VP[ADVP=n, AGR=?a, CASE=?c, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=?p, VERB_ONLY=n, VERBLESS=n, WH=n]/V[AGR=?a, ARGS=?arg, CASE=?c, FINITE=?f, MOOD=?m]
VP[ADVP=y, AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, POS=VP, PREDICATE=?p, WH=n]/?x[POS=AdvP] -> VP[AGR=?a, CASE=?c, DE=?d, FINITE=?f, MOOD=?m, PREDICATE=?p, VERBLESS=n] AdvP[DE=n, WH=n]/?x[POS=AdvP]

#########################
#### Adjunct phrases ####
#########################

#### Prepositional phrases ####

# PPs as P + NP

PP[DE=?d, POS=PP, REL=n, WH=?w] -> P[ARGS=[ARG_ACC=y], POSTPOS=n] C[DE=?d, SECOND_POSITION=y] NP[CASE=accusative, DE=n, FUSED_REL=n, REL=n, WH=?w]
PP[DE=?d, POS=PP, REL=n, WH=?w] -> P[ARGS=[ARG_DAT=y], POSTPOS=n] C[DE=?d, SECOND_POSITION=y] NP[CASE=dative, DE=n, FUSED_REL=n, REL=n, WH=?w]
PP[DE=?d, POS=PP, REL=n, WH=?w] -> P[ARGS=[ARG_GEN=y], POSTPOS=n] C[DE=?d, SECOND_POSITION=y] NP[CASE=genitive, DE=n, FUSED_REL=n, REL=n, WH=?w]

# Postpositions

PP[DE=?d, POS=PP, REL=n, WH=?w] -> NP[CASE=genitive, DE=?d, FUSED_REL=n, REL=n, WH=?w] P[ARGS=[ARG_GEN=y], POSTPOS=y]

# Other minor types

PP[DE=?d, POS=PP, REL=n, WH=n] -> P[ARGS=[ARG_NONE=y], POSTPOS=n] C[DE=?d, SECOND_POSITION=y]
PP[DE=n, POS=PP, REL=n, WH=n] -> 'ei)s_P-------' 'to/_D----ASN' 'pe/ran_B-------'
PP[DE=n, POS=PP, REL=n, WH=n] -> 'e(/ws_P-------' 'e(pta/kis_B-------'
PP[DE=n, POS=PP, REL=n, WH=y] -> 'e(/ws_P-------' 'po/te_B-------'
PP[DE=?d, POS=PP, REL=y, WH=y] -> P[ARGS=[ARG_ACC=y], POSTPOS=n] C[DE=?d, SECOND_POSITION=y] NP[CASE=accusative, DE=n, FUSED_REL=n, REL=y, WH=y] IP[REL=n, WH=n]
PP[DE=?d, POS=PP, REL=y, WH=y] -> P[ARGS=[ARG_DAT=y], POSTPOS=n] C[DE=?d, SECOND_POSITION=y] NP[CASE=dative, DE=n, FUSED_REL=n, REL=y, WH=y] IP[REL=n, WH=n]

#### Adverbs and adverby things ####

# Adverb phrases

AdvP[DE=?d, POS=AdvP, PREDICATE=?p, REL=n, WH=?w] -> B[ADJUNCT=y, NEGATIVE=n, PREDICATE=?p, WH=?w] C[DE=?d, SECOND_POSITION=y]
AdvP[DE=?d, POS=AdvP, PREDICATE=n, REL=n, WH=y] -> I[AGR=[GENDER=neuter], CASE=accusative, WH=y] C[DE=?d, SECOND_POSITION=y]
AdvP[DE=?d, POS=AdvP, PREDICATE=n, REL=n, WH=n] -> A[ADVERB=y, AGR=[GENDER=neuter], CASE=accusative] C[DE=?d, SECOND_POSITION=y]
AdvP[DE=?d, POS=AdvP, PREDICATE=n, REL=n, WH=n] -> D[AGR=?a, CASE=accusative] C[DE=?d, SECOND_POSITION=y] A[ADVERB=y, AGR=?a[GENDER=neuter], CASE=accusative]
AdvP[DE=?d, POS=AdvP, PREDICATE=y, REL=?r, WH=?w] -> PP[DE=?d, REL=?r, WH=?w]
AdvP[DE=?d, POS=AdvP, PREDICATE=n, REL=n, WH=n] -> VP[CASE=nominative, DE=?d, MOOD=participle, VERBLESS=n]
AdvP[DE=?d, POS=AdvP, PREDICATE=n, REL=n, WH=n] -> VP[AGR=?agr, CASE=genitive, DE=?d, MOOD=participle, VERBLESS=n]
AdvP[DE=?d, POS=AdvP, PREDICATE=n, REL=n, WH=n] -> VP[AGR=?agr, CASE=genitive, DE=?d, MOOD=participle, VERBLESS=n] NP[AGR=?agr, CASE=genitive]
AdvP[DE=?d, POS=AdvP, PREDICATE=?p, REL=n, WH=?w] -> 'kai/_B-------' AdvP[DE=?d, PREDICATE=?p, WH=?w]
AdvP[DE=?d, POS=AdvP, PREDICATE=n, REL=n, WH=n] -> 'eu)qu/s_A----NSM' C[DE=?d, SECOND_POSITION=y]

# Vocatives as adjuncts

AdjP[COMMA_OPTIONAL=n, DE=?d, WH=n] -> NP[CASE=vocative, DE=?d, FUSED_REL=n]

# Particles

AdjP[COMMA_OPTIONAL=y, DE=?d, WH=n] -> L[ADJUNCT=y, NEGATIVE=n] C[DE=?d, SECOND_POSITION=y]

#######################
#### Miscellaneous ####
#######################

# Negation particles

Neg -> B[NEGATIVE=y]
Neg -> L[NEGATIVE=y]

