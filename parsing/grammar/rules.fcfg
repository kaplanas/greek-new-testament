#################################
#### Top-level constructions ####
#################################

# Basic sentence types

S -> J[STANDALONE=y]
S -> L[STANDALONE=y]
S -> NP[WH=y, NULLED=n, CASE=nominative]
S -> NP[NULLED=n, CASE=genitive]
S -> NP[NULLED=n, CASE=dative]
S -> NP[NULLED=n, CASE=accusative]
S -> NP[NULLED=n, CASE=vocative]
S -> NP[NULLED=n, CASE=nominative] NP[NULLED=n, DE=n, CASE=dative]
S -> Neg C[SECOND_POSITION=y] NP[NULLED=n]
S -> D[CASE=nominative] C[SECOND_POSITION=y]
S -> A
S -> AdvP
S -> C[SECOND_POSITION=n] AdvP
S -> IP
S -> C[SECOND_POSITION=n] IP[KAI=n]
S -> VP[VERBLESS=n, NULLED=n, FINITE=n]
S -> 'o(/ra_V2PAM-S-' 'mh/_B-------'

# Subject + predicate

IP[VERBLESS=?vl, DE=?d, KAI=n, MOOD=?m, AGR=?a, SUBJECT=n] -> VP[FINITE=y, VERBLESS=?vl, NULLED=n, DE=?d, KAI=n, MOOD=?m, AGR=?a]
IP[VERBLESS=?vl, DE=?d, KAI=n, MOOD=?m, AGR=?a, SUBJECT=n]/?NP -> VP[FINITE=y, VERBLESS=?vl, NULLED=n, DE=?d, KAI=n, MOOD=?m, AGR=?a]/?NP

IP[VERBLESS=?vl, DE=?d, KAI=?k, MOOD=?m, AGR=?a, SUBJECT=y] -> NP[NULLED=n, WH=n, DE=?d, CASE=nominative, AGR=?a] VP[FINITE=y, VERBLESS=?vl, NULLED=n, DE=n, KAI=?k, MOOD=?m, AGR=?a]
IP[VERBLESS=?vl, DE=?d, KAI=?k, MOOD=?m, AGR=?a, SUBJECT=y]/?x -> NP[NULLED=n, WH=n, DE=?d, CASE=nominative, AGR=?a] VP[FINITE=y, VERBLESS=?vl, NULLED=n, DE=n, KAI=?k, MOOD=?m, AGR=?a]/?x

IP[VERBLESS=?vl, DE=?d, KAI=?k, MOOD=?m, AGR=?a, SUBJECT=y] -> NP[NULLED=n, WH=n, DE=?d, CASE=nominative, AGR=?a[PERSON=3, NUMBER=plural, GENDER=neuter]] VP[FINITE=y, VERBLESS=?vl, NULLED=n, DE=n, KAI=?k, MOOD=?m, AGR=[PERSON=3, NUMBER=singular, GENDER=neuter]]
IP[VERBLESS=?vl, DE=?d, KAI=?k, MOOD=?m, AGR=?a, SUBJECT=y]/?x -> NP[NULLED=n, WH=n, DE=?d, CASE=nominative, AGR=?a[PERSON=3, NUMBER=plural, GENDER=neuter]] VP[FINITE=y, VERBLESS=?vl, NULLED=n, DE=n, KAI=?k, MOOD=?m, AGR=[PERSON=3, NUMBER=singular, GENDER=neuter]]/?x

IP[VERBLESS=?vl, DE=?d, KAI=n, MOOD=?m, AGR=?a, SUBJECT=y] -> VP[FINITE=y, VERBLESS=?vl, NULLED=n, DE=?d, KAI=n, MOOD=?m, AGR=?a] NP[NULLED=n, WH=n, DE=n, CASE=nominative, AGR=?a]
IP[VERBLESS=?vl, DE=?d, KAI=?k, MOOD=?m, AGR=?a, SUBJECT=y]/?x -> VP[FINITE=y, VERBLESS=?vl, NULLED=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a]/?x NP[NULLED=n, WH=n, DE=n, CASE=nominative, AGR=?a]

IP[VERBLESS=?vl, DE=?d, KAI=n, MOOD=?m, AGR=?a, SUBJECT=y] -> VP[FINITE=y, VERBLESS=?vl, NULLED=n, DE=?d, KAI=n, MOOD=?m, AGR=[PERSON=3, NUMBER=singular, GENDER=neuter]] NP[NULLED=n, WH=n, DE=n, CASE=nominative, AGR=?a[PERSON=3, NUMBER=plural, GENDER=neuter]]
IP[VERBLESS=?vl, DE=?d, KAI=?k, MOOD=?m, AGR=?a, SUBJECT=y]/?x -> VP[FINITE=y, VERBLESS=?vl, NULLED=n, DE=?d, KAI=?k, MOOD=?m, AGR=[PERSON=3, NUMBER=singular, GENDER=neuter]]/?x NP[NULLED=n, WH=n, DE=n, CASE=nominative, AGR=?a[PERSON=3, NUMBER=plural, GENDER=neuter]]

# Wh constructions and other movement

IP[VERBLESS=?vl, DE=?d, KAI=?k, MOOD=?m, AGR=?a, SUBJECT=y] -> NP[WH=y, DE=?d, CASE=nominative, AGR=?a] VP[FINITE=y, VERBLESS=?vl, NULLED=n, DE=n, KAI=?k, MOOD=?m, AGR=?a]
IP[VERBLESS=?vl, DE=?d, KAI=?k, MOOD=?m, AGR=?a, SUBJECT=?s] -> NP[WH=y, DE=?d, CASE=?c] IP[VERBLESS=?vl, DE=n, KAI=?k, MOOD=?m, AGR=?a, SUBJECT=?s]/NP[CASE=?c]
IP[VERBLESS=?vl, DE=?d, KAI=?k, MOOD=?m, AGR=?a, SUBJECT=?s] -> PredP[WH=y, DE=?d] IP[VERBLESS=?vl, DE=n, KAI=?k, MOOD=?m, AGR=?a, SUBJECT=?s]/PredP
IP[VERBLESS=?vl, DE=?d, KAI=?k, MOOD=?m, AGR=?a, SUBJECT=?s] -> AdvP[WH=y, DE=?d] IP[VERBLESS=?vl, DE=n, KAI=?k, MOOD=?m, AGR=?a, SUBJECT=?s]

IP[VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a, SUBJECT=y] -> V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=?arg] C[SECOND_POSITION=y, DE=?d] IP[VERBLESS=n, DE=n, KAI=?k, MOOD=?m, AGR=?a, SUBJECT=y]/?V[FINITE=?f, MOOD=?m, AGR=?a, ARGS=?arg]

# Things that can get tacked onto sentences

S -> AdjP S
S -> S AdjP

IP[VERBLESS=?vl, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> VP[VERBLESS=n, NULLED=n, DE=?d, MOOD=participle, CASE=nominative, AGR=?a] IP[VERBLESS=?vl, DE=n, KAI=?k, MOOD=?m, AGR=?a]
IP[VERBLESS=?vl, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> IP[VERBLESS=?vl, DE=?d, KAI=?k, MOOD=?m, AGR=?a] VP[VERBLESS=n, NULLED=n, DE=n, MOOD=participle, CASE=nominative, AGR=?a]

# Negated constructions

IP[VERBLESS=?vl, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> 'mh/_B-------' C[SECOND_POSITION=y, DE=?d] IP[VERBLESS=?vl, DE=n, KAI=?k, MOOD=?m, AGR=?a]

# Conjunction

IP[VERBLESS=n, DE=?d, KAI=n] -> V[FINITE=y, NULLED=n, NEGATIVE=n, KAI=n] C[SECOND_POSITION=y, DE=?d] V[FINITE=y, NULLED=n, NEGATIVE=n, KAI=n]
IP[VERBLESS=?vl, DE=?d, KAI=n, MOOD=?m, AGR=?a] -> IP[VERBLESS=?vl, DE=?d, KAI=n, MOOD=?m, AGR=?a] C[SECOND_POSITION=n] IP[VERBLESS=?vl, DE=n, KAI=n]

######################
#### Noun phrases ####
######################

# Noun phrases

NP[STANDALONE_ADJ=?sa, WH=?w, DE=?d, CASE=?c, AGR=?a] -> N_like[STANDALONE_ADJ=?sa, WH=?w, CASE=?c, AGR=?a] C[SECOND_POSITION=y, DE=?d]
NP[STANDALONE_ADJ=?sa, WH=n, DE=?d, CASE=?c, AGR=?a] -> D[CASE=?c, AGR=?a] C[SECOND_POSITION=y, DE=?d] N_like[STANDALONE_ADJ=?sa, WH=n, CASE=?c, AGR=?a[PERSON=3]]
NP[STANDALONE_ADJ=?sa, WH=?w, DE=?d, CASE=?c, AGR=?a] -> A[CASE=?c, AGR=?a] C[SECOND_POSITION=y, DE=?d] N_like[STANDALONE_ADJ=?sa, WH=?w, CASE=?c, AGR=?a]
NP[STANDALONE_ADJ=n, WH=n, DE=?d, CASE=?c, AGR=?a[PERSON=3]] -> D[CASE=?c, AGR=?a] DE[SECOND_POSITION=y, DE=?d] A[CASE=?c, AGR=?a]
NP[STANDALONE_ADJ=n, WH=n, DE=?d, CASE=?c, AGR=?a] -> E[CASE=?c, AGR=?a] C[SECOND_POSITION=y, DE=?d]
NP[STANDALONE_ADJ=n, WH=n, DE=?d, CASE=?c, AGR=?a] -> R[CASE=?c, AGR=?a] C[SECOND_POSITION=y, DE=?d]

NP[STANDALONE_ADJ=?sa, NULLED=n, WH=n, DE=?d, CASE=?c, AGR=?a] -> NP[STANDALONE_ADJ=?sa, NULLED=n, WH=n, DE=?d, CASE=?c, AGR=?a] NP[NULLED=n, CASE=genitive]

# Traces and other empty things

NP[CASE=?c]/NP[CASE=?c] ->
NP[NULLED=y, WH=n] ->
VP[NULLED=y] ->
PredP/PredP ->
V/V ->

C[SECOND_POSITION=y, DE=n] ->

# Noun-like vocabulary items

N_like[WH=n, CASE=?c, AGR=?a] -> N[CASE=?c, AGR=?a]
N_like[WH=n, CASE=?c, AGR=?a] -> M[CASE=?c, AGR=?a]
N_like[WH=?w, CASE=?c, AGR=?a] -> I[CASE=?c, WH=?w, AGR=?a]
N_like[STANDALONE_ADJ=y, WH=n, CASE=?c, AGR=?a] -> A[STANDALONE=y, CASE=?c, AGR=?a]
N_like[WH=n, CASE=?c, AGR=?a] -> V[FINITE=n, NULLED=n, KAI=n, MOOD=participle, CASE=?c, AGR=?a]

######################
#### Verb phrases ####
######################

# Ordinary verbs

VP[FINITE=?f, VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_NONE=y]] C[SECOND_POSITION=y, DE=?d]

VP[FINITE=?f, VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_ACC=y]] C[SECOND_POSITION=y, DE=?d] NP[NULLED=n, WH=n, DE=n, CASE=accusative]
VP[FINITE=?f, VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> NP[NULLED=n, WH=n, DE=?d, CASE=accusative] V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_ACC=y]]
VP[FINITE=?f, VERBLESS=n, DE=n, KAI=?k, MOOD=?m, AGR=?a]/?x -> V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_ACC=y]] NP[CASE=accusative]/?x

VP[FINITE=?f, VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_DAT=y]] C[SECOND_POSITION=y, DE=?d] NP[NULLED=n, WH=n, DE=n, CASE=dative]
VP[FINITE=?f, VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> NP[NULLED=n, WH=n, DE=?d, CASE=dative] V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_DAT=y]]
VP[FINITE=?f, VERBLESS=n, DE=n, KAI=?k, MOOD=?m, AGR=?a]/?x -> V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_DAT=y]] NP[CASE=dative]/?x

VP[FINITE=?f, VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_GEN=y]] C[SECOND_POSITION=y, DE=?d] NP[NULLED=n, WH=n, DE=n, CASE=genitive]
VP[FINITE=?f, VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> NP[NULLED=n, WH=n, DE=?d, CASE=genitive] V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_GEN=y]]
VP[FINITE=?f, VERBLESS=n, DE=n, KAI=?k, MOOD=?m, AGR=?a]/?x -> V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_GEN=y]]/?x NP[NULLED=n, WH=n, DE=n, CASE=genitive]
VP[FINITE=?f, VERBLESS=n, DE=n, KAI=?k, MOOD=?m, AGR=?a]/?x -> V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_GEN=y]] NP[CASE=genitive]/?x

VP[FINITE=?f, VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_INF=y]] C[SECOND_POSITION=y, DE=?d] VP[NULLED=n, VERBLESS=n, DE=n, MOOD=infinitive]
VP[FINITE=?f, VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> VP[NULLED=n, VERBLESS=n, DE=?d, MOOD=infinitive] V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_INF=y]]

VP[FINITE=?f, VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_OTI=y]] C[SECOND_POSITION=y, DE=?d] 'o(/ti_C-------' VP[NULLED=n, VERBLESS=n, DE=n, FINITE=y]

VP[FINITE=?f, VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> NP[NULLED=n, WH=n, DE=?d, CASE=accusative] PredP[WH=n, DE=n, CASE=accusative] V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_ACC_ACC=y]]
VP[FINITE=?f, VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> NP[NULLED=n, WH=n, DE=?d, CASE=accusative] NP[WH=n, DE=n, CASE=accusative] V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_ACC_ACC=y]]
VP[FINITE=?f, VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> PredP[NULLED=n, WH=n, DE=?d, CASE=accusative] V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_ACC_ACC=y]] NP[NULLED=n, WH=n, DE=n, CASE=accusative]
VP[FINITE=?f, VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a]/?x -> NP[CASE=accusative]/?x NP[NULLED=n, WH=n, DE=?d, CASE=accusative] V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_ACC_ACC=y]]

VP[FINITE=?f, VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_ACC_DAT=y]] C[SECOND_POSITION=y, DE=?d] NP[NULLED=n, WH=n, DE=n, CASE=dative] NP[NULLED=n, WH=n, DE=n, CASE=accusative]
VP[FINITE=?f, VERBLESS=n, DE=n, KAI=?k, MOOD=?m, AGR=?a]/?x -> V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_ACC_DAT=y]] NP[NULLED=n, WH=n, DE=n, CASE=dative] NP[CASE=accusative]/?x

VP[FINITE=?f, VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_DAT_GEN=y]] C[SECOND_POSITION=y, DE=?d] NP[NULLED=n, WH=n, DE=n, CASE=genitive] NP[NULLED=n, WH=n, DE=n, CASE=dative]

VP[FINITE=?f, VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_DAT_INF=y]] C[SECOND_POSITION=y, DE=?d] NP[NULLED=n, WH=n, CASE=dative] VP[NULLED=n, VERBLESS=n, DE=n, MOOD=infinitive]

# Copula verbs

PredP[WH=?w, DE=?d, CASE=?c] -> NP[STANDALONE_ADJ=n, WH=?w, NULLED=n, DE=?d, CASE=?c]
PredP[WH=n, DE=?d, CASE=?c, AGR=[NUMBER=?n, GENDER=?g]] -> A[STANDALONE=n, CASE=?c, AGR=[NUMBER=?n, GENDER=?g]] C[SECOND_POSITION=y, DE=?d]
PredP[WH=n, DE=?d, CASE=?c, AGR=[NUMBER=?n, GENDER=?g]] -> VP[FINITE=N, VERBLESS=n, NULLED=n, DE=?d, KAI=n, MOOD=participle, CASE=?c, AGR=[NUMBER=?n, GENDER=?g]]

VP[FINITE=y, VERBLESS=y, DE=?d, KAI=n, MOOD=indicative, AGR=?a] -> PredP[WH=n, DE=?d, CASE=nominative, AGR=?a]
VP[FINITE=?f, VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> V[COPULA=y, FINITE=?f, KAI=?k, MOOD=?m, AGR=?a] C[SECOND_POSITION=y, DE=?d] PredP[WH=n, DE=n, CASE=nominative, AGR=?a]
VP[FINITE=?f, VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> PredP[WH=n, DE=?d, CASE=nominative, AGR=?a] V[COPULA=y, FINITE=?f, KAI=?k, MOOD=?m, AGR=?a]
VP[FINITE=?f, VERBLESS=n, DE=n, KAI=?k, MOOD=?m, AGR=?a]/?x -> V[COPULA=y, FINITE=?f, KAI=?k, MOOD=?m, AGR=?a] PredP[CASE=nominative, AGR=?a]/?x

# Pre-verb particles

V[FINITE=?f, NEGATIVE=y, KAI=n, MOOD=?m, COPULA=?c, AGR=?a, ARGS=?args] -> 'ou)_B-------' V[FINITE=?f, NEGATIVE=n, KAI=n, MOOD=?m, COPULA=?c, AGR=?a, ARGS=?args]
V[FINITE=?f, KAI=y, MOOD=?m, COPULA=?c, AGR=?a, ARGS=?args] -> 'kai/_C-------' V[FINITE=?f, KAI=n, MOOD=?m, COPULA=?c, AGR=?a, ARGS=?args]

# Adjuncts

VP[FINITE=?f, VERBLESS=?vl, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> AdvP[WH=n, DE=?d] VP[FINITE=?f, VERBLESS=?vl, NULLED=n, DE=n, KAI=?k, MOOD=?m, AGR=?a]
VP[FINITE=?f, VERBLESS=?vl, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> VP[FINITE=?f, VERBLESS=?vl, NULLED=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a] AdvP[WH=n, DE=n]

#########################
#### Adjunct phrases ####
#########################

# Prepositional phrases

PP[WH=?w, DE=?d] -> P[ACC_ARG=y, POSTPOS=n] C[SECOND_POSITION=y, DE=?d] NP[WH=?w, DE=n, NULLED=n, CASE=accusative]
PP[WH=?w, DE=?d] -> P[DAT_ARG=y, POSTPOS=n] C[SECOND_POSITION=y, DE=?d] NP[WH=?w, DE=n, NULLED=n, CASE=dative]
PP[WH=?w, DE=?d] -> P[GEN_ARG=y, POSTPOS=n] C[SECOND_POSITION=y, DE=?d] NP[WH=?w, DE=n, NULLED=n, CASE=genitive]

PP[WH=?w, DE=?d] -> NP[WH=?w, DE=?d, NULLED=n, CASE=genitive] P[GEN_ARG=y, POSTPOS=y]

PP[WH=n, DE=n] -> 'e(/ws_P-------' 'e(pta/kis_B-------'

AdvP[WH=?w, DE=?d] -> PP[WH=?w, DE=?d]

# Adverbs and adverby things

AdvP[WH=?w, DE=?d] -> B[WH=?w, NEGATIVE=n] C[SECOND_POSITION=y, DE=?d]
AdvP[WH=?w, DE=?d] -> I[WH=?w, CASE=accusative, AGR=[GENDER=neuter]] C[SECOND_POSITION=y, DE=?d]
AdvP[WH=n, DE=?d] -> A[CASE=accusative, AGR=[GENDER=neuter]] C[SECOND_POSITION=y, DE=?d]

# Vocatives as adjuncts

AdjP[WH=n, DE=?d] -> NP[NULLED=n, DE=?d, CASE=vocative]

# Particles

AdjP[WH=n, DE=?d] -> L[NEGATIVE=n] C[SECOND_POSITION=y, DE=?d]

#######################
#### Miscellaneous ####
#######################

# Negation particles

Neg -> B[NEGATIVE=y]
Neg -> L[NEGATIVE=y]

