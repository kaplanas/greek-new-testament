#################################
#### Top-level constructions ####
#################################

# Basic sentence types

S -> J[STANDALONE=y]
S -> L[STANDALONE=y]
S -> NP[NULLED=n]
S -> NP[NULLED=n, CASE=nominative] NP[NULLED=n, DE=n, CASE=dative]
S -> Neg C[SECOND_POSITION=y] NP[NULLED=n]
S -> D[CASE=nominative] C[SECOND_POSITION=y]
S -> A
S -> AdvP
S -> IP
S -> VP[VERBLESS=n, NULLED=n, FINITE=n]
S -> 'o(/ra_V2PAM-S-' 'mh/_B-------'

# Subject + predicate

IP[VERBLESS=?vl, DE=?d, MOOD=?m, AGR=?a] -> VP[FINITE=y, VERBLESS=?vl, NULLED=n, DE=?d, MOOD=?m, AGR=?a]
IP[VERBLESS=?vl, DE=?d, MOOD=?m, AGR=?a]/?x -> VP[FINITE=y, VERBLESS=?vl, NULLED=n, DE=?d, MOOD=?m, AGR=?a]/?x

IP[VERBLESS=?vl, DE=?d, MOOD=?m, AGR=?a] -> NP[NULLED=n, WH=n, DE=?d, CASE=nominative, AGR=?a] VP[FINITE=y, VERBLESS=?vl, NULLED=n, DE=n, MOOD=?m, AGR=?a]
IP[VERBLESS=?vl, DE=?d, MOOD=?m, AGR=?a]/?x -> NP[NULLED=n, WH=n, DE=?d, CASE=nominative, AGR=?a] VP[FINITE=y, VERBLESS=?vl, NULLED=n, DE=n, MOOD=?m, AGR=?a]/?x

IP[VERBLESS=?vl, DE=?d, MOOD=?m, AGR=?a] -> NP[NULLED=n, WH=n, DE=?d, CASE=nominative, AGR=?a[PERSON=3, NUMBER=plural, GENDER=neuter]] VP[FINITE=y, VERBLESS=?vl, NULLED=n, DE=n, MOOD=?m, AGR=[PERSON=3, NUMBER=singular, GENDER=neuter]]
IP[VERBLESS=?vl, DE=?d, MOOD=?m, AGR=?a]/?x -> NP[NULLED=n, WH=n, DE=?d, CASE=nominative, AGR=?a[PERSON=3, NUMBER=plural, GENDER=neuter]] VP[FINITE=y, VERBLESS=?vl, NULLED=n, DE=n, MOOD=?m, AGR=[PERSON=3, NUMBER=singular, GENDER=neuter]]/?x

IP[VERBLESS=?vl, DE=?d, MOOD=?m, AGR=?a] -> VP[FINITE=y, VERBLESS=?vl, NULLED=n, DE=?d, MOOD=?m, AGR=?a] NP[NULLED=n, WH=n, DE=n, CASE=nominative, AGR=?a]
IP[VERBLESS=?vl, DE=?d, MOOD=?m, AGR=?a]/?x -> VP[FINITE=y, VERBLESS=?vl, NULLED=n, DE=?d, MOOD=?m, AGR=?a]/?x NP[NULLED=n, WH=n, DE=n, CASE=nominative, AGR=?a]

IP[VERBLESS=?vl, DE=?d, MOOD=?m, AGR=?a] -> VP[FINITE=y, VERBLESS=?vl, NULLED=n, DE=?d, MOOD=?m, AGR=[PERSON=3, NUMBER=singular, GENDER=neuter]] NP[NULLED=n, WH=n, DE=n, CASE=nominative, AGR=?a[PERSON=3, NUMBER=plural, GENDER=neuter]]
IP[VERBLESS=?vl, DE=?d, MOOD=?m, AGR=?a]/?x -> VP[FINITE=y, VERBLESS=?vl, NULLED=n, DE=?d, MOOD=?m, AGR=[PERSON=3, NUMBER=singular, GENDER=neuter]]/?x NP[NULLED=n, WH=n, DE=n, CASE=nominative, AGR=?a[PERSON=3, NUMBER=plural, GENDER=neuter]]

# Wh constructions

IP[VERBLESS=?vl, DE=?d, MOOD=?m, AGR=?a] -> NP[WH=y, DE=?d, CASE=nominative, AGR=?a] VP[FINITE=y, VERBLESS=?vl, NULLED=n, DE=n, MOOD=?m, AGR=?a]
IP[VERBLESS=?vl, DE=?d, MOOD=?m, AGR=?a] -> NP[WH=y, DE=?d, CASE=?c] IP[VERBLESS=?vl, DE=n, MOOD=?m]/NP[CASE=?c]
IP[VERBLESS=?vl, DE=?d, MOOD=?m, AGR=?a] -> PredP[WH=y, DE=?d] IP[VERBLESS=?vl, DE=n, MOOD=?m, AGR=?a]/PredP
IP[VERBLESS=?vl, DE=?d, MOOD=?m, AGR=?a] -> AdvP[WH=y, DE=?d] IP[VERBLESS=?vl, DE=n, MOOD=?m, AGR=?a]

# Things that can get tacked onto sentences

S -> C[SECOND_POSITION=n] S
S -> AdjP S
S -> S AdjP

IP[VERBLESS=?vl, DE=?d, MOOD=?m, AGR=?a] -> VP[VERBLESS=n, NULLED=n, DE=?d, MOOD=participle, CASE=nominative, AGR=?a] IP[VERBLESS=?vl, DE=n, MOOD=?m, AGR=?a]
IP[VERBLESS=?vl, DE=?d, MOOD=?m, AGR=?a] -> IP[VERBLESS=?vl, DE=?d, MOOD=?m, AGR=?a] VP[VERBLESS=n, NULLED=n, DE=n, MOOD=participle, CASE=nominative, AGR=?a]

# Conjunction

IP[VERBLESS=n, DE=?d] -> V[FINITE=y, NULLED=n] C[SECOND_POSITION=y, DE=?d] V[FINITE=y, NULLED=n]

######################
#### Noun phrases ####
######################

# Noun phrases

NP[WH=?w, DE=?d, CASE=?c, AGR=?a] -> N_like[WH=?w, CASE=?c, AGR=?a] C[SECOND_POSITION=y, DE=?d]
NP[WH=n, DE=?d, CASE=?c, AGR=?a] -> D[CASE=?c, AGR=?a] C[SECOND_POSITION=y, DE=?d] N_like[WH=n, CASE=?c, AGR=?a[PERSON=3]]
NP[WH=?w, DE=?d, CASE=?c, AGR=?a] -> A[CASE=?c, AGR=?a] C[SECOND_POSITION=y, DE=?d] N_like[WH=?w, CASE=?c, AGR=?a]
NP[WH=n, DE=?d, CASE=?c, AGR=?a[PERSON=3]] -> D[CASE=?c, AGR=?a] DE[SECOND_POSITION=y, DE=?d] A[CASE=?c, AGR=?a]

# Traces and other empty things

NP[CASE=?c]/NP[CASE=?c] ->
NP[NULLED=y, WH=n] ->
VP[NULLED=y] ->
PredP/PredP ->

C[SECOND_POSITION=y, DE=n] ->

# Noun-like vocabulary items

N_like[WH=n, CASE=?c, AGR=?a] -> N[CASE=?c, AGR=?a]
N_like[WH=n, CASE=?c, AGR=?a] -> E[CASE=?c, AGR=?a]
N_like[WH=n, CASE=?c, AGR=?a] -> M[CASE=?c, AGR=?a]
N_like[WH=?w, CASE=?c, AGR=?a] -> I[CASE=?c, WH=?w, AGR=?a]
N_like[WH=n, CASE=?c, AGR=?a] -> A[STANDALONE=y, CASE=?c, AGR=?a]

######################
#### Verb phrases ####
######################

# Ordinary verbs

VP[FINITE=?f, VERBLESS=n, DE=?d, MOOD=?m, AGR=?a] -> V[FINITE=?f, MOOD=?m, AGR=?a, ARGS=none] C[SECOND_POSITION=y, DE=?d]

VP[FINITE=?f, VERBLESS=n, DE=?d, MOOD=?m, AGR=?a] -> V[FINITE=?f, MOOD=?m, AGR=?a, ARGS=acc] C[SECOND_POSITION=y, DE=?d] NP[WH=n, DE=n, CASE=accusative]
VP[FINITE=?f, VERBLESS=n, DE=?d, MOOD=?m, AGR=?a] -> NP[NULLED=n, WH=n, DE=?d, CASE=accusative] V[FINITE=?f, MOOD=?m, AGR=?a, ARGS=acc]
VP[FINITE=?f, VERBLESS=n, DE=n, MOOD=?m, AGR=?a]/?x -> V[FINITE=?f, MOOD=?m, AGR=?a, ARGS=acc] NP[CASE=accusative]/?x

VP[FINITE=?f, VERBLESS=n, DE=?d, MOOD=?m, AGR=?a] -> V[FINITE=?f, MOOD=?m, AGR=?a, ARGS=dat] C[SECOND_POSITION=y, DE=?d] NP[WH=n, DE=n, CASE=dative]
VP[FINITE=?f, VERBLESS=n, DE=?d, MOOD=?m, AGR=?a] -> NP[NULLED=n, WH=n, DE=?d, CASE=dative] V[FINITE=?f, MOOD=?m, AGR=?a, ARGS=dat]
VP[FINITE=?f, VERBLESS=n, DE=n, MOOD=?m, AGR=?a]/?x -> V[FINITE=?f, MOOD=?m, AGR=?a, ARGS=dat] NP[CASE=dative]/?x

VP[FINITE=?f, VERBLESS=n, DE=?d, MOOD=?m, AGR=?a] -> V[FINITE=?f, MOOD=?m, AGR=?a, ARGS=gen] C[SECOND_POSITION=y, DE=?d] NP[WH=n, DE=n, CASE=genitive]
VP[FINITE=?f, VERBLESS=n, DE=?d, MOOD=?m, AGR=?a] -> NP[NULLED=n, WH=n, DE=?d, CASE=genitive] V[FINITE=?f, MOOD=?m, AGR=?a, ARGS=gen]
VP[FINITE=?f, VERBLESS=n, DE=n, MOOD=?m, AGR=?a]/?x -> V[FINITE=?f, MOOD=?m, AGR=?a, ARGS=gen] NP[CASE=genitive]/?x

VP[FINITE=?f, VERBLESS=n, DE=?d, MOOD=?m, AGR=?a] -> V[FINITE=?f, MOOD=?m, AGR=?a, ARGS=inf] C[SECOND_POSITION=y, DE=?d] VP[VERBLESS=n, DE=n, MOOD=infinitive]
VP[FINITE=?f, VERBLESS=n, DE=?d, MOOD=?m, AGR=?a] -> VP[NULLED=n, VERBLESS=n, DE=?d, MOOD=infinitive] V[FINITE=?f, MOOD=?m, AGR=?a, ARGS=inf]

VP[FINITE=?f, VERBLESS=n, DE=?d, MOOD=?m, AGR=?a] -> V[FINITE=?f, MOOD=?m, AGR=?a, ARGS=acc_dat] C[SECOND_POSITION=y, DE=?d] NP[WH=n, DE=n, CASE=accusative] NP[WH=n, DE=n, CASE=dative]
VP[FINITE=?f, VERBLESS=n, DE=n, MOOD=?m, AGR=?a]/?x -> V[FINITE=?f, MOOD=?m, AGR=?a, ARGS=acc_dat] NP[WH=n, DE=n, CASE=dative] NP[CASE=accusative]/?x

VP[FINITE=?f, VERBLESS=n, DE=?d, MOOD=?m, AGR=?a] -> V[FINITE=?f, MOOD=?m, AGR=?a, ARGS=acc_gen] C[SECOND_POSITION=y, DE=?d] NP[WH=n, DE=n, CASE=accusative] NP[WH=n, DE=n, CASE=genitive]

VP[FINITE=?f, VERBLESS=n, DE=?d, MOOD=?m, AGR=?a] -> V[FINITE=?f, MOOD=?m, AGR=?a, ARGS=acc_inf] C[SECOND_POSITION=y, DE=?d] NP[WH=n, DE=n, CASE=accusative] VP[VERBLESS=n, DE=n, MOOD=infinitive]
VP[FINITE=?f, VERBLESS=n, DE=?d, MOOD=?m, AGR=?a]/?x -> V[FINITE=?f, MOOD=?m, AGR=?a, ARGS=acc_inf] NP[CASE=accusative]/?x VP[VERBLESS=n, DE=n, MOOD=infinitive]

VP[FINITE=?f, VERBLESS=n, DE=?d, MOOD=?m, AGR=?a] -> V[FINITE=?f, MOOD=?m, AGR=?a, ARGS=acc_dat_inf] C[SECOND_POSITION=y, DE=?d] NP[WH=n, DE=n, CASE=accusative] NP[WH=n, CASE=dative] VP[VERBLESS=n, DE=n, MOOD=infinitive]
VP[FINITE=?f, VERBLESS=n, DE=?d, MOOD=?m, AGR=?a] -> VP[NULLED=n, VERBLESS=n, DE=?d, MOOD=infinitive] V[FINITE=?f, MOOD=?m, AGR=?a, ARGS=acc_dat_inf]

# Copula verbs

PredP[WH=?w, DE=?d, CASE=nominative, AGR=[NUMBER=?n, GENDER=?g]] -> NP[WH=?w, NULLED=n, DE=?d, CASE=nominative, AGR=[NUMBER=?n, GENDER=?g]]
PredP[WH=n, DE=?d, CASE=nominative, AGR=[NUMBER=?n, GENDER=?g]] -> A[CASE=nominative, AGR=[NUMBER=?n, GENDER=?g]] C[SECOND_POSITION=y, DE=?d]

VP[FINITE=y, VERBLESS=y, DE=?d, MOOD=indicative, AGR=?a] -> PredP[WH=n, DE=?d, CASE=nominative, AGR=?a]
VP[FINITE=y, VERBLESS=n, DE=?d, MOOD=?m, AGR=?a] -> V[COPULA=y, FINITE=y, MOOD=?m, AGR=?a] C[SECOND_POSITION=y, DE=?d] PredP[WH=n, DE=n, CASE=nominative, AGR=?a]
VP[FINITE=y, VERBLESS=n, DE=?d, MOOD=?m, AGR=?a] -> PredP[WH=n, DE=?d, CASE=nominative, AGR=?a] V[COPULA=y, FINITE=y, MOOD=?m, AGR=?a]
VP[FINITE=y, VERBLESS=n, DE=n, MOOD=?m, AGR=?a]/?x -> V[COPULA=y, FINITE=y, MOOD=?m, AGR=?a] PredP[AGR=?a]/?x

# Negated verbs

V[FINITE=?f, MOOD=?m, AGR=?a, ARGS=?args] -> Neg V[FINITE=?f, MOOD=?m, AGR=?a, ARGS=?args]

# Adjuncts

VP[FINITE=?f, VERBLESS=?vl, DE=?d, MOOD=?m, AGR=?a] -> AdvP[WH=n, DE=?d] VP[FINITE=?f, VERBLESS=?vl, NULLED=n, DE=n, MOOD=?m, AGR=?a]
VP[FINITE=?f, VERBLESS=?vl, DE=?d, MOOD=?m, AGR=?a] -> VP[FINITE=?f, VERBLESS=?vl, NULLED=n, DE=?d, MOOD=?m, AGR=?a] AdvP[WH=n, DE=n]

#########################
#### Adjunct phrases ####
#########################

# Prepositional phrases

PP[WH=?w, DE=?d] -> P[ACC_ARG=y, POSTPOS=n] C[SECOND_POSITION=y, DE=?d] NP[WH=?w, DE=n, NULLED=n, CASE=accusative]
PP[WH=?w, DE=?d] -> P[DAT_ARG=y, POSTPOS=n] C[SECOND_POSITION=y, DE=?d] NP[WH=?w, DE=n, NULLED=n, CASE=dative]
PP[WH=?w, DE=?d] -> P[GEN_ARG=y, POSTPOS=n] C[SECOND_POSITION=y, DE=?d] NP[WH=?w, DE=n, NULLED=n, CASE=genitive]

PP[WH=?w, DE=?d] -> NP[WH=?w, DE=?d, NULLED=n, CASE=genitive] P[GEN_ARG=y, POSTPOS=y]

PP[WH=n, DE=n] -> 'e(/ws_P-------' 'e(pta/kis_B-------'

AdvP[WH=?n, DE=?d] -> PP[WH=?w, DE=?d]

# Adverbs and adverby things

AdvP[WH=?w, DE=?d] -> B[WH=?w, NEGATIVE=n] C[SECOND_POSITION=y, DE=?d]
AdvP[WH=?w, DE=?d] -> I[WH=?w, CASE=accusative, AGR=[GENDER=neuter]] C[SECOND_POSITION=y, DE=?d]
AdvP[WH=n, DE=?d] -> A[CASE=accusative, AGR=[GENDER=neuter]] C[SECOND_POSITION=y, DE=?d]

# Vocatives as adjuncts

AdjP[WH=n, DE=?d] -> N[CASE=vocative] C[SECOND_POSITION=y, DE=?d]

# Particles

AdjP[WH=n, DE=?d] -> L[NEGATIVE=n] C[SECOND_POSITION=y, DE=?d]

#######################
#### Miscellaneous ####
#######################

# Negation particles

Neg -> B[NEGATIVE=y]
Neg -> L[NEGATIVE=y]

