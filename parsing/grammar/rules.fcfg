#################################
#### Top-level constructions ####
#################################

# Single-constituent sentences

S -> J[STANDALONE=y]
S -> L[STANDALONE=y]
S -> NP
S -> A
S -> AdvP
S -> IP

# Subject + predicate

IP[MOOD=?m, TENSE=?t] -> VP[FINITE=y, MOOD=?m, TENSE=?t]

IP[MOOD=?m, TENSE=?t] -> NP[WH=n, CASE=nominative, PERSON=?p, NUMBER=?n, GENDER=?g] VP[FINITE=y, MOOD=?m, TENSE=?t, PERSON=?p, NUMBER=?n, GENDER=?g]
IP[MOOD=?m, TENSE=?t] -> NP[WH=n, CASE=nominative, PERSON=3, NUMBER=plural, GENDER=neuter] VP[FINITE=y, MOOD=?m, TENSE=?t, PERSON=3, NUMBER=singular, GENDER=neuter]
IP[MOOD=?m, TENSE=?t] -> VP[FINITE=y, MOOD=?m, TENSE=?t, PERSON=?p, NUMBER=?n, GENDER=?g] NP[WH=n, CASE=nominative, PERSON=?p, NUMBER=?n, GENDER=?g]
IP[MOOD=?m, TENSE=?t] -> VP[FINITE=y, MOOD=?m, TENSE=?t, PERSON=3, NUMBER=singular, GENDER=neuter] NP[WH=n, CASE=nominative, PERSON=3, NUMBER=plural, GENDER=neuter]

IP[MOOD=?m, TENSE=?t]/?x -> VP[FINITE=y, MOOD=?m, TENSE=?t, PERSON=?p, NUMBER=?n, GENDER=?g]/?x
IP[MOOD=?m, TENSE=?t]/?x -> NP[WH=n, CASE=nominative, PERSON=?p, NUMBER=?n, GENDER=?g] VP[FINITE=y, MOOD=?m, TENSE=?t, PERSON=?p, NUMBER=?n, GENDER=?g]/?x

# Wh constructions

IP[MOOD=?m, TENSE=?t] -> NP[WH=y, CASE=nominative, PERSON=3, NUMBER=?n, GENDER=?g] VP[FINITE=y, MOOD=?m, TENSE=?t, PERSON=3, NUMBER=?n, GENDER=?g]
IP[MOOD=?m, TENSE=?t] -> NP[WH=y, CASE=?c] IP[MOOD=?m, TENSE=?t]/NP[CASE=?c]
IP[MOOD=?m, TENSE=?t] -> AdvP[WH=y] IP[MOOD=?m, TENSE=?t]

# Things that can get tacked onto sentences

S -> C[SECOND_POSITION=n] S
S -> AdjP S
S -> S AdjP

# Conjunction

IP[MOOD=?m, TENSE=?t] -> IP[MOOD=?m, TENSE=?t] IP[MOOD=?m, TENSE=?t]

######################
#### Noun phrases ####
######################

# Noun phrases

NP[WH=?w, CASE=?c, PERSON=?p, NUMBER=?n, GENDER=?g] -> N_like[WH=?w, CASE=?c, PERSON=?p, NUMBER=?n, GENDER=?g]
NP[WH=n, CASE=?c, PERSON=3, NUMBER=?n, GENDER=?g] -> D[CASE=?c, NUMBER=?n, GENDER=?g] N_like[WH=n, CASE=?c, PERSON=3, NUMBER=?n, GENDER=?g]
NP[WH=n, CASE=?c, PERSON=3, NUMBER=?n, GENDER=?g] -> D[CASE=?c, NUMBER=?n, GENDER=?g] A[CASE=?c, NUMBER=?n, GENDER=?g]

# Traces

NP[CASE=?c]/NP[CASE=?c] ->

# Noun-like vocabulary items

N_like[WH=n, CASE=?c, PERSON=3, NUMBER=?n, GENDER=?g] -> N[CASE=?c, NUMBER=?n, GENDER=?G]
N_like[WH=n, CASE=?c, PERSON=?p, NUMBER=?n, GENDER=?g] -> E[CASE=?c, PERSON=?p, NUMBER=?n, GENDER=?g]
N_like[WH=n, CASE=?c, PERSON=3, NUMBER=?n, GENDER=?g] -> M[CASE=?c, NUMBER=?n, GENDER=?g]
N_like[WH=?w, CASE=?c, PERSON=3, NUMBER=?n, GENDER=?g] -> I[CASE=?c, WH=?w, NUMBER=?n, GENDER=?g]
N_like[WH=n, CASE=?c, PERSON=3, NUMBER=?n, GENDER=?g] -> A[STANDALONE=y, CASE=?c, NUMBER=?n, GENDER=?g]

######################
#### Verb phrases ####
######################

# Ordinary verbs

VP[FINITE=?f, MOOD=?m, TENSE=?t, PERSON=?p, NUMBER=?n] -> V[FINITE=?f, MOOD=?m, TENSE=?t, PERSON=?p, NUMBER=?n]

VP[FINITE=?f, MOOD=?m, TENSE=?t, PERSON=?p, NUMBER=?n] -> V[FINITE=?f, MOOD=?m, TENSE=?t, PERSON=?p, NUMBER=?n, ACC_ARG=y] NP[WH=n, CASE=accusative]
VP[FINITE=?f, MOOD=?m, TENSE=?t, PERSON=?p, NUMBER=?n] -> NP[WH=n, CASE=accusative] V[FINITE=?f, MOOD=?m, TENSE=?t, PERSON=?p, NUMBER=?n, ACC_ARG=y]
VP[FINITE=?f, MOOD=?m, TENSE=?t, PERSON=?p, NUMBER=?n]/?x -> V[FINITE=?f, MOOD=?m, TENSE=?t, PERSON=?p, NUMBER=?n, ACC_ARG=y] NP[CASE=accusative]/?x

VP[FINITE=?f, MOOD=?m, TENSE=?t, PERSON=?p, NUMBER=?n] -> NP[WH=n, CASE=dative] V[FINITE=?f, MOOD=?m, TENSE=?t, PERSON=?p, NUMBER=?n, DAT_ARG=y]
VP[FINITE=?f, MOOD=?m, TENSE=?t, PERSON=?p, NUMBER=?n] -> V[FINITE=?f, MOOD=?m, TENSE=?t, PERSON=?p, NUMBER=?n, DAT_ARG=y] NP[WH=n, CASE=dative]
VP[FINITE=?f, MOOD=?m, TENSE=?t, PERSON=?p, NUMBER=?n]/?x -> V[FINITE=?f, MOOD=?m, TENSE=?t, PERSON=?p, NUMBER=?n, DAT_ARG=y] NP[CASE=dative]/?x

VP[FINITE=?f, MOOD=?m, TENSE=?t, PERSON=?p, NUMBER=?n] -> NP[WH=n, CASE=genitive] V[FINITE=?f, MOOD=?m, TENSE=?t, PERSON=?p, NUMBER=?n, GEN_ARG=y]
VP[FINITE=?f, MOOD=?m, TENSE=?t, PERSON=?p, NUMBER=?n] -> V[FINITE=?f, MOOD=?m, TENSE=?t, PERSON=?p, NUMBER=?n, GEN_ARG=y] NP[WH=n, CASE=genitive]
VP[FINITE=?f, MOOD=?m, TENSE=?t, PERSON=?p, NUMBER=?n]/?x -> V[FINITE=?f, MOOD=?m, TENSE=?t, PERSON=?p, NUMBER=?n, GEN_ARG=y] NP[CASE=genitive]/?x

VP[FINITE=?f, MOOD=?m, TENSE=?t, PERSON=?p, NUMBER=?n]/?x -> V[FINITE=?f, MOOD=?m, TENSE=?t, PERSON=?p, NUMBER=?n, ACC_ARG=y, DAT_ARG=y] NP[WH=n, CASE=dative] NP[WH=n, CASE=accusative]/?x

# Copula verbs

PredP[WH=n, CASE=nominative, PERSON=3, NUMBER=?n, GENDER=?g] -> NP[WH=n, CASE=nominative, PERSON=3, NUMBER=?n, GENDER=?g]
PredP[WH=n, CASE=nominative, PERSON=3, NUMBER=?n, GENDER=?g] -> A[CASE=nominative, NUMBER=?n, GENDER=?g]

VP[FINITE=y, MOOD=indicative, TENSE=indicative, NUMBER=?n, GENDER=?g] -> PredP[WH=n, CASE=nominative, PERSON=3, NUMBER=?n, GENDER=?g]
VP[FINITE=y, MOOD=?m, PERSON=?p, TENSE=?t, NUMBER=?n, GENDER=?g] -> V[COPULA=y, FINITE=y, MOOD=?m, TENSE=?t, PERSON=?p, NUMBER=?n] PredP[WH=n, CASE=nominative, PERSON=3, NUMBER=?n, GENDER=?g]
VP[FINITE=y, MOOD=?m, PERSON=?p, TENSE=?t, NUMBER=?n, GENDER=?g] -> PredP[WH=n, CASE=nominative, PERSON=3, NUMBER=?n, GENDER=?g] V[COPULA=y, FINITE=y, MOOD=?m, TENSE=?t, PERSON=?p, NUMBER=?n]

# Negated verbs

V[FINITE=?f, MOOD=?m, TENSE=?t, PERSON=?p, NUMBER=?n, GENDER=?g, ACC_ARG=?x, DAT_ARG=?y, GEN_ARG=?z] -> Neg V[FINITE=?f, MOOD=?m, TENSE=?t, PERSON=?p, NUMBER=?n, GENDER=?g, ACC_ARG=?x, DAT_ARG=?y, GEN_ARG=?z]

# Adjuncts

VP[FINITE=?f, MOOD=?m, TENSE=?t, PERSON=?p, NUMBER=?n, GENDER=?g] -> AdvP[WH=n] VP[FINITE=?f, MOOD=?m, TENSE=?t, PERSON=?p, NUMBER=?n, GENDER=?g]

#########################
#### Adjunct phrases ####
#########################

# Adverbs and adverby things

AdvP[WH=?w] -> B[WH=?w, NEGATIVE=n]
AdvP[WH=?w] -> I[WH=?w, CASE=accusative, GENDER=neuter]
AdvP[WH=n] -> A[CASE=accusative, GENDER=neuter]

# Vocatives as adjuncts

AdjP[WH=n] -> N[CASE=vocative]

#######################
#### Miscellaneous ####
#######################

# Negation particles

Neg -> B[NEGATIVE=y]
Neg -> L[NEGATIVE=y]

