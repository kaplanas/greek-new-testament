#################################
#### Top-level constructions ####
#################################

# Single-constituent sentences

S -> J[STANDALONE=y]
S -> L[STANDALONE=y]
S -> NP[NULLABLE=n]
S -> A
S -> AdvP
S -> IP

# Subject + predicate

IP[VERBLESS=?vl, MOOD=?m] -> NP[NULLABLE=y, WH=n, CASE=nominative, AGR=?a] VP[FINITE=y, VERBLESS=?vl, MOOD=?m, AGR=?a]
IP[VERBLESS=?vl, MOOD=?m] -> NP[NULLABLE=n, WH=n, CASE=nominative, AGR=[PERSON=3, NUMBER=plural, GENDER=neuter]] VP[FINITE=y, VERBLESS=?vl, MOOD=?m, AGR=[PERSON=3, NUMBER=singular, GENDER=neuter]]
IP[VERBLESS=?vl, MOOD=?m] -> VP[FINITE=y, VERBLESS=?vl, MOOD=?m, AGR=?a] NP[NULLABLE=n, WH=n, CASE=nominative, AGR=?a]
IP[VERBLESS=?vl, MOOD=?m] -> VP[FINITE=y, VERBLESS=?vl, MOOD=?m, AGR=[PERSON=3, NUMBER=singular, GENDER=neuter]] NP[NULLABLE=n, WH=n, CASE=nominative, AGR=[PERSON=3, NUMBER=plural, GENDER=neuter]]

# Wh constructions

IP[VERBLESS=?vl, MOOD=?m] -> NP[WH=y, CASE=nominative, AGR=?a] VP[FINITE=y, VERBLESS=?vl, MOOD=?m, AGR=?a]
IP[VERBLESS=?vl, MOOD=?m] -> NP[WH=y, CASE=?c] IP[VERBLESS=?vl, MOOD=?m]/NP[CASE=?c]
IP[VERBLESS=?vl, MOOD=?m] -> AdvP[WH=y] IP[VERBLESS=?vl, MOOD=?m]

# Things that can get tacked onto sentences

S -> C[SECOND_POSITION=n] S
S -> AdjP S
S -> S AdjP

# Conjunction

IP[VERBLESS=n, MOOD=?m] -> IP[VERBLESS=n, MOOD=?m] IP[VERBLESS=n, MOOD=?m]

######################
#### Noun phrases ####
######################

# Noun phrases

NP[WH=?w, CASE=?c, AGR=?a] -> N_like[WH=?w, CASE=?c, AGR=?a]
NP[WH=n, CASE=?c, AGR=[PERSON=3, NUMBER=?n, GENDER=?g]] -> D[CASE=?c, NUMBER=?n, GENDER=?g] N_like[WH=n, CASE=?c, AGR=[PERSON=3, NUMBER=?n, GENDER=?g]]
NP[WH=n, CASE=?c, AGR=[PERSON=3, NUMBER=?n, GENDER=?g]] -> D[CASE=?c, NUMBER=?n, GENDER=?g] A[CASE=?c, AGR=[NUMBER=?n, GENDER=?g]]

# Traces and other empty things

NP[CASE=?c]/NP[CASE=?c] ->
NP[NULLABLE=y, WH=n] ->

# Noun-like vocabulary items

N_like[WH=n, CASE=?c, AGR=?a] -> N[CASE=?c, AGR=?a]
N_like[WH=n, CASE=?c, AGR=?a] -> E[CASE=?c, AGR=?a]
N_like[WH=n, CASE=?c, AGR=?a] -> M[CASE=?c, AGR=?a]
N_like[WH=?w, CASE=?c, AGR=?a] -> I[CASE=?c, WH=?w, AGR=?a]
N_like[WH=n, CASE=?c, AGR=?a] -> A[STANDALONE=y, CASE=?c, AGR=?a]

######################
#### Verb phrases ####
######################

# Ordinary verbs

VP[FINITE=?f, VERBLESS=n, MOOD=?m, AGR=?a] -> V[FINITE=?f, MOOD=?m, AGR=?a, ACC_ARG=n, DAT_ARG=n, GEN_ARG=n]

VP[FINITE=?f, VERBLESS=n, MOOD=?m, AGR=?a] -> V[FINITE=?f, MOOD=?m, AGR=?a, ACC_ARG=y] NP[NULLABLE=y, WH=n, CASE=accusative]
VP[FINITE=?f, VERBLESS=n, MOOD=?m, AGR=?a] -> NP[NULLABLE=n, WH=n, CASE=accusative] V[FINITE=?f, MOOD=?m, AGR=?a, ACC_ARG=y]
VP[FINITE=?f, VERBLESS=n, MOOD=?m, AGR=?a]/?x -> V[FINITE=?f, MOOD=?m, AGR=?a, ACC_ARG=y] NP[CASE=accusative]/?x

VP[FINITE=?f, VERBLESS=n, MOOD=?m, AGR=?a] -> V[FINITE=?f, MOOD=?m, AGR=?a, DAT_ARG=y] NP[NULLABLE=y, WH=n, CASE=dative]
VP[FINITE=?f, VERBLESS=n, MOOD=?m, AGR=?a] -> NP[NULLABLE=n, WH=n, CASE=dative] V[FINITE=?f, MOOD=?m, AGR=?a, DAT_ARG=y]
VP[FINITE=?f, VERBLESS=n, MOOD=?m, AGR=?a]/?x -> V[FINITE=?f, MOOD=?m, AGR=?a, DAT_ARG=y] NP[CASE=dative]/?x

VP[FINITE=?f, VERBLESS=n, MOOD=?m, AGR=?a] -> V[FINITE=?f, MOOD=?m, AGR=?a, GEN_ARG=y] NP[NULLABLE=y, WH=n, CASE=genitive]
VP[FINITE=?f, VERBLESS=n, MOOD=?m, AGR=?a] -> NP[NULLABLE=n, WH=n, CASE=genitive] V[FINITE=?f, MOOD=?m, AGR=?a, GEN_ARG=y]
VP[FINITE=?f, VERBLESS=n, MOOD=?m, AGR=?a]/?x -> V[FINITE=?f, MOOD=?m, AGR=?a, GEN_ARG=y] NP[CASE=genitive]/?x

VP[FINITE=?f, VERBLESS=n, MOOD=?m, AGR=?a]/?x -> V[FINITE=?f, MOOD=?m, AGR=?a, ACC_ARG=y, DAT_ARG=y] NP[NULLABLE=y, WH=n, CASE=dative] NP[CASE=accusative]/?x

# Copula verbs

PredP[WH=n, CASE=nominative, AGR=[NUMBER=?n, GENDER=?g]] -> NP[NULLABLE=n, WH=n, CASE=nominative, AGR=[PERSON=3, NUMBER=?n, GENDER=?g]]
PredP[WH=n, CASE=nominative, AGR=[NUMBER=?n, GENDER=?g]] -> A[CASE=nominative, AGR=[PERSON=3, NUMBER=?n, GENDER=?g]]

VP[FINITE=y, VERBLESS=y, MOOD=indicative, AGR=?a] -> PredP[WH=n, CASE=nominative, AGR=?a]
VP[FINITE=y, VERBLESS=n, MOOD=?m, AGR=?a] -> V[COPULA=y, FINITE=y, MOOD=?m, AGR=?a] PredP[WH=n, CASE=nominative, AGR=?a]
VP[FINITE=y, VERBLESS=n, MOOD=?m, AGR=?a] -> PredP[WH=n, CASE=nominative, AGR=?a] V[COPULA=y, FINITE=y, MOOD=?m, AGR=?a]

# Negated verbs

V[FINITE=?f, MOOD=?m, AGR=?a, ACC_ARG=?x, DAT_ARG=?y, GEN_ARG=?z] -> Neg V[FINITE=?f, MOOD=?m, AGR=?a, ACC_ARG=?x, DAT_ARG=?y, GEN_ARG=?z]

# Adjuncts

VP[FINITE=?f, VERBLESS=?vl, MOOD=?m, AGR=?a] -> AdvP[WH=n] VP[FINITE=?f, VERBLESS=?vl, MOOD=?m, AGR=?a]

#########################
#### Adjunct phrases ####
#########################

# Prepositional phrases

PP[WH=?w] -> P[ACC_ARG=y, POSTPOS=n] NP[WH=?w, CASE=accusative]
PP[WH=?w] -> P[DAT_ARG=y, POSTPOS=n] NP[WH=?w, CASE=dative]
PP[WH=?w] -> P[GEN_ARG=y, POSTPOS=n] NP[WH=?w, CASE=genitive]

PP[WH=?w] -> NP[WH=?w, CASE=genitive] P[GEN_ARG=y, POSTPOS=y]

AdvP[WH=?n] -> PP[WH=?w]

# Adverbs and adverby things

AdvP[WH=?w] -> B[WH=?w, NEGATIVE=n]
AdvP[WH=?w] -> I[WH=?w, CASE=accusative, AGR=[GENDER=neuter]]
AdvP[WH=n] -> A[CASE=accusative, AGR=[GENDER=neuter]]

# Vocatives as adjuncts

AdjP[WH=n] -> N[CASE=vocative]

#######################
#### Miscellaneous ####
#######################

# Negation particles

Neg -> B[NEGATIVE=y]
Neg -> L[NEGATIVE=y]

