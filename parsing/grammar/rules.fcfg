#################################
#### Top-level constructions ####
#################################

# Basic sentence types

S -> J[STANDALONE=y]
S -> L[STANDALONE=y]
S -> NP[WH=y, CASE=nominative, FUSED_REL=n]
S -> NP[CASE=genitive, FUSED_REL=n]
#S -> NP[CASE=dative, FUSED_REL=n]
S -> NP[CASE=accusative, FUSED_REL=n]
S -> NP[CASE=vocative, FUSED_REL=n]
S -> Neg C[SECOND_POSITION=y] NP[FUSED_REL=n]
S -> D[CASE=nominative] C[SECOND_POSITION=y]
S -> A
S -> AdvP
S -> C[SECOND_POSITION=n] AdvP
S -> IP
S -> C[SECOND_POSITION=n] IP[KAI=n]
S -> VP[VERBLESS=n, FINITE=n]
S -> 'o(/ra_V2PAM-S-' 'mh/_B-------'
S -> 'ou)ai/_J-------' NP[DE=n, CASE=dative, FUSED_REL=n]

# Subject + predicate

IP[VERBLESS=?vl, DE=?d, KAI=n, MOOD=?m, AGR=?a, SUBJECT=n] -> VP[FINITE=y, VERBLESS=?vl, DE=?d, KAI=n, MOOD=?m, AGR=?a]
IP[VERBLESS=?vl, DE=?d, KAI=n, MOOD=?m, AGR=?a, SUBJECT=n]/?x -> VP[FINITE=y, VERBLESS=?vl, DE=?d, KAI=n, MOOD=?m, AGR=?a]/?x

IP[VERBLESS=?vl, DE=?d, KAI=?k, MOOD=?m, AGR=?a, SUBJECT=y] -> NP[WH=n, FUSED_REL=n, DE=?d, CASE=nominative, AGR=?a] VP[FINITE=y, VERBLESS=?vl, DE=n, KAI=?k, MOOD=?m, AGR=?a]
IP[VERBLESS=?vl, DE=?d, KAI=?k, MOOD=?m, AGR=?a, SUBJECT=y]/?x[CASE=?c] -> NP[WH=n, FUSED_REL=n, DE=?d, CASE=nominative, AGR=?a] VP[FINITE=y, VERBLESS=?vl, DE=n, KAI=?k, MOOD=?m, AGR=?a]/?x[CASE=?c]

IP[VERBLESS=?vl, DE=?d, KAI=?k, MOOD=?m, AGR=?a, SUBJECT=y] -> NP[WH=n, FUSED_REL=n, DE=?d, CASE=nominative, AGR=?a[PERSON=3, NUMBER=plural, GENDER=neuter]] VP[FINITE=y, VERBLESS=?vl, DE=n, KAI=?k, MOOD=?m, AGR=[PERSON=3, NUMBER=singular, GENDER=neuter]]
IP[VERBLESS=?vl, DE=?d, KAI=?k, MOOD=?m, AGR=?a, SUBJECT=y]/?x[CASE=?c] -> NP[WH=n, FUSED_REL=n, DE=?d, CASE=nominative, AGR=?a[PERSON=3, NUMBER=plural, GENDER=neuter]] VP[FINITE=y, VERBLESS=?vl, DE=n, KAI=?k, MOOD=?m, AGR=[PERSON=3, NUMBER=singular, GENDER=neuter]]/?x[CASE=?c]

IP[VERBLESS=?vl, DE=?d, KAI=n, MOOD=?m, AGR=?a, SUBJECT=y] -> VP[FINITE=y, VERBLESS=?vl, DE=?d, KAI=n, MOOD=?m, AGR=?a] NP[WH=n, FUSED_REL=n, DE=n, CASE=nominative, AGR=?a]
IP[VERBLESS=?vl, DE=?d, KAI=?k, MOOD=?m, AGR=?a, SUBJECT=y]/?x[CASE=?c, POS=N] -> VP[FINITE=y, VERBLESS=?vl, DE=?d, KAI=?k, MOOD=?m, AGR=?a]/?x[CASE=?c, POS=N] NP[WH=n, FUSED_REL=n, DE=n, CASE=nominative, AGR=?a]

IP[VERBLESS=?vl, DE=?d, KAI=n, MOOD=?m, AGR=?a, SUBJECT=y] -> VP[FINITE=y, VERBLESS=?vl, DE=?d, KAI=n, MOOD=?m, AGR=[PERSON=3, NUMBER=singular, GENDER=neuter]] NP[WH=n, FUSED_REL=n, DE=n, CASE=nominative, AGR=?a[PERSON=3, NUMBER=plural, GENDER=neuter]]
IP[VERBLESS=?vl, DE=?d, KAI=?k, MOOD=?m, AGR=?a, SUBJECT=y]/?x[CASE=?c, POS=N] -> VP[FINITE=y, VERBLESS=?vl, DE=?d, KAI=?k, MOOD=?m, AGR=[PERSON=3, NUMBER=singular, GENDER=neuter]]/?x[CASE=?c, POS=N] NP[WH=n, FUSED_REL=n, DE=n, CASE=nominative, AGR=?a[PERSON=3, NUMBER=plural, GENDER=neuter]]

IP[VERBLESS=n, DE=y, KAI=?k, MOOD=?m, AGR=?a, SUBJECT=y] -> D[CASE=nominative, AGR=?a] 'de/_C-------' VP[FINITE=y, VERBLESS=n, DE=n, KAI=?k, MOOD=?m, AGR=?a]

# Wh constructions and other movement

IP[VERBLESS=?vl, DE=?d, KAI=?k, MOOD=?m, AGR=?a, SUBJECT=y] -> NP[WH=y, DE=?d, CASE=nominative, AGR=?a] VP[FINITE=y, VERBLESS=?vl, DE=n, KAI=?k, MOOD=?m, AGR=?a]
IP[VERBLESS=?vl, DE=?d, KAI=?k, MOOD=?m, AGR=?a, SUBJECT=?s] -> NP[WH=y, DE=?d, CASE=?c] IP[VERBLESS=?vl, DE=n, KAI=?k, MOOD=?m, AGR=?a, SUBJECT=?s]/NP[CASE=?c]
IP[VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a, SUBJECT=y] -> PredP[WH=y, DE=?d] IP[VERBLESS=n, DE=n, KAI=?k, MOOD=?m, AGR=?a, SUBJECT=y]/PredP
IP[VERBLESS=y, DE=?d, KAI=?k, MOOD=?m, AGR=?a, SUBJECT=y] -> PredP[WH=y, DE=?d] IP[VERBLESS=y, DE=n, KAI=?k, MOOD=?m, AGR=?a, SUBJECT=y]/PredP
IP[VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a, SUBJECT=m] -> PredP[WH=y, DE=?d] IP[VERBLESS=n, DE=n, KAI=?k, MOOD=?m, AGR=?a, SUBJECT=n]/PredP
IP[VERBLESS=?vl, DE=?d, KAI=?k, MOOD=?m, AGR=?a, SUBJECT=?s] -> AdvP[WH=y, DE=?d] IP[VERBLESS=?vl, DE=n, KAI=?k, MOOD=?m, AGR=?a, SUBJECT=?s]/AdvP
IP[VERBLESS=?vl, DE=n, KAI=?k, MOOD=?m, AGR=?a, SUBJECT=?s] -> A[WH=?w, CASE=?c] IP[VERBLESS=?vl, DE=n, KAI=?k, MOOD=?m, AGR=?a, SUBJECT=?s]/A[WH=?w, CASE=?c]

IP[VERBLESS=?vl, DE=?d, KAI=?k, MOOD=?m, AGR=?a, SUBJECT=y] -> NP[WH=n, DE=?d, CASE=?c] IP[VERBLESS=?vl, DE=n, KAI=?k, MOOD=?m, AGR=?a, SUBJECT=y]/NP[CASE=?c]

IP[VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a, SUBJECT=y] -> V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=?arg] C[SECOND_POSITION=y, DE=?d] IP[VERBLESS=n, DE=n, KAI=?k, MOOD=?m, AGR=?a, SUBJECT=y]/?V[FINITE=?f, MOOD=?m, AGR=?a, ARGS=?arg]

# Things that can get tacked onto sentences

S -> AdjP S
S -> S AdjP

IP[VERBLESS=?vl, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> VP[VERBLESS=n, DE=?d, MOOD=participle, CASE=nominative, AGR=?a] IP[VERBLESS=?vl, DE=n, KAI=?k, MOOD=?m, AGR=?a]
IP[VERBLESS=?vl, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> IP[VERBLESS=?vl, DE=?d, KAI=?k, MOOD=?m, AGR=?a] VP[VERBLESS=n, DE=n, MOOD=participle, CASE=nominative, AGR=?a]

# Negated constructions

IP[VERBLESS=?vl, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> 'mh/_B-------' C[SECOND_POSITION=y, DE=?d] IP[VERBLESS=?vl, DE=n, KAI=?k, MOOD=?m, AGR=?a]

# Conjunction

IP[VERBLESS=n, DE=?d, KAI=n] -> VP[FINITE=y, DE=?d, VERBLESS=n, NEGATIVE=n, KAI=n, MOOD=imperative] VP[FINITE=y, DE=n, VERBLESS=n, NEGATIVE=n, KAI=n]
IP[VERBLESS=?vl, DE=?d, KAI=n, MOOD=?m, AGR=?a] -> IP[VERBLESS=?vl, DE=?d, KAI=n, MOOD=?m, AGR=?a] C[SECOND_POSITION=n] IP[VERBLESS=?vl, DE=n, KAI=n]

######################
#### Noun phrases ####
######################

# Noun phrases

NP[STANDALONE_ADJ=?sa, WH=?w, DE=?d, KAI=n, CASE=?c, AGR=?a, POS=N] -> N_like[STANDALONE_ADJ=?sa, WH=?w, CASE=?c, AGR=?a] C[SECOND_POSITION=y, DE=?d]
NP[STANDALONE_ADJ=?sa, WH=n, DE=?d, KAI=n, CASE=?c, AGR=?a, POS=N] -> D[CASE=?c, AGR=?a] C[SECOND_POSITION=y, DE=?d] N_like[STANDALONE_ADJ=?sa, WH=n, CASE=?c, AGR=?a[PERSON=3]]
NP[STANDALONE_ADJ=?sa, WH=?w, DE=?d, KAI=n, CASE=?c, AGR=?a, POS=N] -> A[WH=n, CASE=?c, AGR=?a] C[SECOND_POSITION=y, DE=?d] N_like[STANDALONE_ADJ=?sa, WH=?w, CASE=?c, AGR=?a]
NP[STANDALONE_ADJ=?sa, WH=?w, DE=?d, KAI=n, CASE=?c, AGR=?a, POS=N] -> A[WH=?w, CASE=?c, AGR=?a] C[SECOND_POSITION=y, DE=?d] N_like[STANDALONE_ADJ=?sa, WH=n, CASE=?c, AGR=?a]
NP[STANDALONE_ADJ=n, WH=n, DE=?d, KAI=n, CASE=?c, AGR=?a[PERSON=3], POS=N] -> D[CASE=?c, AGR=?a] DE[SECOND_POSITION=y, DE=?d] A[CASE=?c, AGR=?a]
NP[STANDALONE_ADJ=n, WH=n, DE=?d, KAI=n, CASE=?c, AGR=?a, POS=N] -> E[CASE=?c, AGR=?a] C[SECOND_POSITION=y, DE=?d]
NP[STANDALONE_ADJ=n, WH=n, DE=?d, KAI=n, CASE=?c, AGR=?a, POS=N] -> R[CASE=?c, AGR=?a] C[SECOND_POSITION=y, DE=?d]
NP[STANDALONE_ADJ=?sa, WH=n, DE=?d, KAI=n, CASE=?c, AGR=?a, POS=N] -> D[CASE=?c, AGR=?a] C[SECOND_POSITION=y, DE=?d] VP[FINITE=n, KAI=n, MOOD=participle, CASE=?c, AGR=?a]

NP[STANDALONE_ADJ=?sa, WH=n, DE=?d, KAI=?k, CASE=?c, AGR=?a, POS=N] -> NP[STANDALONE_ADJ=?sa, WH=n, DE=?d, KAI=?k, CASE=?c, AGR=?a] NP[CASE=genitive]
NP[STANDALONE_ADJ=?sa, WH=n, DE=?d, KAI=n, CASE=?c, AGR=?a, POS=N] -> N_like[STANDALONE_ADJ=?sa, WH=n, DE=?d, CASE=?c, AGR=?a, ARGS=[ARG_DAT=y]] NP[CASE=dative]
NP[STANDALONE_ADJ=n, WH=n, DE=n, KAI=n, CASE=?c, AGR=?a, POS=N] -> N[CASE=?c, AGR=?a, PROPER=y] NP[WH=n, DE=n, CASE=?c, AGR=?a]

NP[STANDALONE_ADJ=n, WH=n, DE=?d, KAI=n, CASE=?c, AGR=?a, POS=N, FUSED_REL=y] -> NP[WH=y, DE=?d, KAI=n, CASE=?c, AGR=?a] IP[DE=n]/NP[CASE=?c, AGR=?a, POS=N]

NP[STANDALONE_ADJ=?sa, WH=?w, DE=?d, KAI=n, CASE=?c, AGR=?a, POS=N]/?x[POS=A] -> A[WH=?w, CASE=?c, AGR=?a]/?x[POS=A] C[SECOND_POSITION=y, DE=?d] N_like[STANDALONE_ADJ=?sa, WH=n, CASE=?c, AGR=?a]

# Traces and other empty things

NP[CASE=?c, POS=N]/NP[CASE=?c, POS=N] ->
PredP/PredP ->
V[POS=V]/V ->
A/A ->
AdvP/AdvP ->

C[SECOND_POSITION=y, DE=n] ->

# Noun-like vocabulary items

N_like[WH=n, CASE=?c, AGR=?a, ARGS=?arg] -> N[CASE=?c, AGR=?a, ARGS=?arg]
N_like[WH=n, CASE=?c, AGR=?a, ARGS=none] -> M[CASE=?c, AGR=?a]
N_like[WH=?w, CASE=?c, AGR=?a, ARGS=none] -> I[CASE=?c, WH=?w, AGR=?a]
N_like[STANDALONE_ADJ=y, WH=n, CASE=?c, AGR=?a, ARGS=none] -> A[STANDALONE=y, CASE=?c, AGR=?a]

# Pre-NP particles

NP[STANDALONE_ADJ=?sa, WH=n, DE=?d, KAI=y, CASE=?c, AGR=?a, POS=N, FUSED_REL=n] -> 'kai/_B-------' NP[STANDALONE_ADJ=?sa, WH=n, DE=?d, KAI=n, CASE=?c, AGR=?a, POS=N, FUSED_REL=n]

######################
#### Verb phrases ####
######################

# Ordinary verbs

VP[FINITE=?f, VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_NONE=y]] C[SECOND_POSITION=y, DE=?d]

VP[FINITE=?f, VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_NOM=y]] C[SECOND_POSITION=y, DE=?d] NP[WH=n, DE=n, CASE=nominative]

VP[FINITE=?f, VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_ACC=y]] C[SECOND_POSITION=y, DE=?d] NP[WH=n, DE=n, CASE=accusative]
VP[FINITE=?f, VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> NP[WH=n, DE=?d, CASE=accusative] V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_ACC=y]]
VP[FINITE=?f, VERBLESS=n, DE=n, KAI=?k, MOOD=?m, AGR=?a]/?NP[CASE=?c] -> V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_ACC=y]] NP[CASE=accusative]/?NP[CASE=?c]

VP[FINITE=?f, VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_DAT=y]] C[SECOND_POSITION=y, DE=?d] NP[WH=n, DE=n, CASE=dative]
VP[FINITE=?f, VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> NP[WH=n, DE=?d, CASE=dative] V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_DAT=y]]
VP[FINITE=?f, VERBLESS=n, DE=n, KAI=?k, MOOD=?m, AGR=?a]/?x[POS=V] -> V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_DAT=y]]/?x[POS=V] NP[WH=n, DE=n, CASE=dative]
VP[FINITE=?f, VERBLESS=n, DE=n, KAI=?k, MOOD=?m, AGR=?a]/?NP[CASE=?c] -> V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_DAT=y]] NP[CASE=dative]/?NP[CASE=?c]

VP[FINITE=?f, VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_GEN=y]] C[SECOND_POSITION=y, DE=?d] NP[WH=n, DE=n, CASE=genitive]
VP[FINITE=?f, VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> NP[WH=n, DE=?d, CASE=genitive] V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_GEN=y]]
VP[FINITE=?f, VERBLESS=n, DE=n, KAI=?k, MOOD=?m, AGR=?a]/?x[POS=V] -> V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_GEN=y]]/?x[POS=V] NP[WH=n, DE=n, CASE=genitive]
VP[FINITE=?f, VERBLESS=n, DE=n, KAI=?k, MOOD=?m, AGR=?a]/?NP[CASE=?c] -> V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_GEN=y]] NP[CASE=genitive]/?NP[CASE=?c]

VP[FINITE=?f, VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_INF=y]] C[SECOND_POSITION=y, DE=?d] VP[VERBLESS=n, DE=n, MOOD=infinitive]
VP[FINITE=?f, VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> VP[VERBLESS=n, DE=?d, MOOD=infinitive] V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_INF=y]]

VP[FINITE=?f, VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_IMP=y]] C[SECOND_POSITION=y, DE=?d] IP[VERBLESS=n, DE=n, MOOD=imperative]

VP[FINITE=?f, VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_OTI=y]] C[SECOND_POSITION=y, DE=?d] 'o(/ti_C-------' VP[VERBLESS=n, DE=n, FINITE=y]

VP[FINITE=?f, VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_IP=y]] C[SECOND_POSITION=y, DE=?d] IP[VERBLESS=n, DE=n, FINITE=y]
VP[FINITE=?f, VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_IP=y]] C[SECOND_POSITION=y, DE=?d] L[STANDALONE=y]

VP[FINITE=?f, VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> NP[WH=n, DE=?d, CASE=accusative] PredP[WH=n, DE=n, CASE=accusative] V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_ACC_ACC=y]]
VP[FINITE=?f, VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> NP[WH=n, DE=?d, CASE=accusative] NP[WH=n, DE=n, CASE=accusative] V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_ACC_ACC=y]]
VP[FINITE=?f, VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> PredP[WH=n, DE=n, CASE=accusative] V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_ACC_ACC=y]] NP[WH=n, DE=n, CASE=accusative]
VP[FINITE=?f, VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a]/?NP[CASE=?c] -> NP[CASE=accusative]/?NP[CASE=?c] NP[WH=n, DE=?d, CASE=accusative] V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_ACC_ACC=y]]
VP[FINITE=?f, VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a]/?NP[CASE=?c] -> NP[CASE=accusative]/?NP[CASE=?c] V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_ACC_ACC=y]] PredP[WH=n, DE=n, CASE=accusative]

VP[FINITE=?f, VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_ACC_DAT=y]] C[SECOND_POSITION=y, DE=?d] NP[WH=n, DE=n, CASE=dative] NP[WH=n, DE=n, CASE=accusative]
VP[FINITE=?f, VERBLESS=n, DE=n, KAI=?k, MOOD=?m, AGR=?a]/?NP[CASE=?c] -> V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_ACC_DAT=y]] NP[WH=n, DE=n, CASE=dative] NP[CASE=accusative]/?NP[CASE=?c]

VP[FINITE=?f, VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_DAT_GEN=y]] C[SECOND_POSITION=y, DE=?d] NP[WH=n, DE=n, CASE=genitive] NP[WH=n, DE=n, CASE=dative]

VP[FINITE=?f, VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> V[FINITE=?f, KAI=?k, MOOD=?m, AGR=?a, ARGS=[ARG_DAT_INF=y]] C[SECOND_POSITION=y, DE=?d] NP[WH=n, CASE=dative] VP[VERBLESS=n, DE=n, MOOD=infinitive]

# Copula verbs

PredP[WH=?w, DE=?d, CASE=?c] -> NP[STANDALONE_ADJ=n, WH=?w, DE=?d, CASE=?c, FUSED_REL=n]
PredP[WH=?w, DE=?d, CASE=?c]/?x[POS=A] -> NP[STANDALONE_ADJ=n, WH=?w, DE=?d, CASE=?c, FUSED_REL=n]/?x[POS=A]
PredP[WH=n, DE=?d, CASE=?c, AGR=[NUMBER=?n, GENDER=?g]] -> A[STANDALONE=n, CASE=?c, AGR=[NUMBER=?n, GENDER=?g]] C[SECOND_POSITION=y, DE=?d]
PredP[WH=n, DE=?d, CASE=?c, AGR=[NUMBER=?n, GENDER=?g]] -> VP[FINITE=N, VERBLESS=n, DE=?d, KAI=n, MOOD=participle, CASE=?c, AGR=[NUMBER=?n, GENDER=?g]]
PredP[WH=n, DE=?d, CASE=?c, AGR=[NUMBER=singular, GENDER=neuter]] -> VP[FINITE=N, VERBLESS=n, DE=?d, KAI=n, MOOD=participle, CASE=?c, AGR=[NUMBER=plural, GENDER=neuter]]
PredP[WH=?w, DE=?d, CASE=nominative] -> PP[WH=?w, DE=?d]

VP[FINITE=y, VERBLESS=y, DE=?d, KAI=n, MOOD=indicative, AGR=?a] -> PredP[WH=n, DE=?d, CASE=nominative, AGR=?a]
VP[FINITE=y, VERBLESS=y, DE=n, KAI=n, MOOD=indicative, AGR=?a]/?x -> PredP[CASE=nominative, AGR=?a]/?x
VP[FINITE=y, VERBLESS=y, DE=?d, KAI=n, MOOD=indicative] -> PredP[WH=n, DE=?d, CASE=dative]
VP[FINITE=?f, VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> V[COPULA=y, FINITE=?f, KAI=?k, MOOD=?m, AGR=?a] C[SECOND_POSITION=y, DE=?d] PredP[WH=n, DE=n, CASE=nominative, AGR=?a]
VP[FINITE=?f, VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> PredP[WH=n, DE=?d, CASE=nominative, AGR=?a] V[COPULA=y, FINITE=?f, KAI=?k, MOOD=?m, AGR=?a]
VP[FINITE=?f, VERBLESS=n, DE=n, KAI=?k, MOOD=?m, AGR=?a]/?x -> V[COPULA=y, FINITE=?f, KAI=?k, MOOD=?m, AGR=?a] PredP[CASE=nominative, AGR=?a]/?x

# Pre-verb particles

V[FINITE=?f, NEGATIVE=y, KAI=n, MOOD=?m, COPULA=?c, AGR=?a, ARGS=?args] -> 'ou)_B-------' V[FINITE=?f, NEGATIVE=n, KAI=n, MOOD=?m, COPULA=?c, AGR=?a, ARGS=?args]
V[FINITE=?f, KAI=y, MOOD=?m, COPULA=?c, AGR=?a, ARGS=?args] -> 'kai/_C-------' V[FINITE=?f, KAI=n, MOOD=?m, COPULA=?c, AGR=?a, ARGS=?args]

# Adjuncts

VP[FINITE=?f, VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> AdvP[WH=n, DE=?d] VP[FINITE=?f, VERBLESS=n, DE=n, KAI=?k, MOOD=?m, AGR=?a]
VP[FINITE=?f, VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a] -> VP[FINITE=?f, VERBLESS=n, DE=?d, KAI=?k, MOOD=?m, AGR=?a] AdvP[WH=n, DE=n]
VP[FINITE=?f, VERBLESS=?vl, DE=?d, KAI=?k, MOOD=?m, AGR=?a]/AdvP -> VP[FINITE=?f, VERBLESS=?vl, DE=?d, KAI=?k, MOOD=?m, AGR=?a]

#########################
#### Adjunct phrases ####
#########################

# Prepositional phrases

PP[WH=?w, DE=?d] -> P[ARGS=[ARG_NONE=y], POSTPOS=n] C[SECOND_POSITION=y, DE=?d]
PP[WH=?w, DE=?d] -> P[ARGS=[ARG_ACC=y], POSTPOS=n] C[SECOND_POSITION=y, DE=?d] NP[WH=?w, DE=n, CASE=accusative]
PP[WH=?w, DE=?d] -> P[ARGS=[ARG_DAT=y], POSTPOS=n] C[SECOND_POSITION=y, DE=?d] NP[WH=?w, DE=n, CASE=dative]
PP[WH=?w, DE=?d] -> P[ARGS=[ARG_GEN=y], POSTPOS=n] C[SECOND_POSITION=y, DE=?d] NP[WH=?w, DE=n, CASE=genitive]

PP[WH=?w, DE=?d] -> NP[WH=?w, DE=?d, CASE=genitive] P[ARGS=[ARG_GEN=y], POSTPOS=y]

PP[WH=n, DE=n] -> 'e(/ws_P-------' 'e(pta/kis_B-------'

AdvP[WH=?w, DE=?d] -> PP[WH=?w, DE=?d]

# Adverbs and adverby things

AdvP[WH=?w, DE=?d] -> B[WH=?w, NEGATIVE=n] C[SECOND_POSITION=y, DE=?d]
AdvP[WH=?w, DE=?d] -> I[WH=?w, CASE=accusative, AGR=[GENDER=neuter]] C[SECOND_POSITION=y, DE=?d]
AdvP[WH=n, DE=?d] -> A[CASE=accusative, AGR=[GENDER=neuter]] C[SECOND_POSITION=y, DE=?d]

# Vocatives as adjuncts

AdjP[WH=n, DE=?d] -> NP[DE=?d, CASE=vocative]

# Particles

AdjP[WH=n, DE=?d] -> L[NEGATIVE=n] C[SECOND_POSITION=y, DE=?d]

#######################
#### Miscellaneous ####
#######################

# Negation particles

Neg -> B[NEGATIVE=y]
Neg -> L[NEGATIVE=y]

