##################
#### Features ####
##################

# AdvP[DE=?d, DIRECTION=?dir, PREDICATE=?pred, PROS=?pros, REL=?r, WH=?w]

###############################
#### Prepositional phrases ####
###############################

#### PPs as P + NP ####

# PPs with nominative, accusative, dative, genitive, and fused relative.
PP[DE=?d, DIRECTION=?dir, PROS=?pros, REL=?r, WH=?w] -> P[ARGS=[ARG_NOM=y], DIRECTION=?dir, POSTPOS=n, PROS=?pros] C[DE=?d, SECOND_POSITION=y] NP[CASE=nominative, DE=n, REL=?r, WH=?w]
PP[DE=?d, DIRECTION=?dir, PROS=?pros, REL=?r, WH=?w] -> P[ARGS=[ARG_ACC=y], DIRECTION=?dir, POSTPOS=n, PROS=?pros] C[DE=?d, SECOND_POSITION=y] NP[CASE=accusative, DE=n, REL=?r, WH=?w]
PP[DE=?d, DIRECTION=?dir, PROS=?pros, REL=n, WH=?w] -> P[ARGS=[ARG_DAT=y], DIRECTION=?dir, POSTPOS=n, PROS=?pros] C[DE=?d, SECOND_POSITION=y] NP[CASE=dative, DE=n, REL=n, WH=?w]
PP[DE=?d, DIRECTION=?dir, PROS=?pros, REL=n, WH=?w] -> P[ARGS=[ARG_GEN=y], DIRECTION=?dir, POSTPOS=n, PROS=?pros] C[DE=?d, SECOND_POSITION=y] NP[CASE=genitive, DE=n, REL=n, WH=?w]
PP[DE=?d, DIRECTION=?dir, PROS=?pros, REL=y, WH=?w] -> P[ARGS=[ARG_FUSED_REL=y], DIRECTION=?dir, POSTPOS=n, PROS=?pros] C[DE=?d, SECOND_POSITION=y] NP[CASE=fused_rel, DE=n, WH=?w]

# Intransitive Ps.
PP[DE=?d, DIRECTION=?dir, PROS=?pros, REL=n, WH=?w] -> P[ARGS=[ARG_NONE=y], DIRECTION=?dir, POSTPOS=n, PROS=?pros] C[DE=?d, SECOND_POSITION=y]

#### Postpositions ####

# The only postposition takes the genitive.
PP[DE=?d, DIRECTION=?dir, POS=PP, PROS=?pros, REL=n, WH=?w] -> NP[CASE=genitive, DE=?d, FUSED_REL=n, PROS=?pros, REL=n, WH=?w] P[ARGS=[ARG_GEN=y], DIRECTION=?dir, POSTPOS=y]

#### Other minor types ####

# ews can take an adverb.
PP[DE=n, DIRECTION=n, POS=PP, PROS=n, REL=n, WH=n] -> 'e(/ws_P-------' B

# Not worth writing a general rule.
PP[DE=n, DIRECTION=y, POS=PP, REL=n, WH=n] -> 'ei)s_P-------' 'to/_D----ASN' 'pe/ran_B-------'

####################################
#### Adverbs and adverby things ####
####################################

#### Adverb phrases ####

# Adverbs.
AdvP[DE=?d, DIRECTION=?dir, PREDICATE=?pred, PROS=n, REL=n, WH=?w] -> B[ADJUNCT=y, DIRECTION=?dir, NEGATIVE=n, PREDICATE=?pred, WH=?w] C[DE=?d, SECOND_POSITION=y]

# Accusatives as adverbs.
AdvP[DE=?d, DIRECTION=n, PREDICATE=n, PROS=n, REL=n, WH=y] -> I[AGR=[GENDER=neuter], CASE=accusative, WH=y] C[DE=?d, SECOND_POSITION=y]
AdvP[DE=?d, PREDICATE=n, PROS=n, REL=n, WH=n] -> A[ADVERB=y, AGR=[GENDER=neuter], CASE=accusative] C[DE=?d, SECOND_POSITION=y]
AdvP[DE=?d, PREDICATE=n, PROS=n, REL=n, WH=n] -> D[AGR=[GENDER=neuter, NUMBER=?n], CASE=accusative] A[ADVERB=y, AGR=[GENDER=neuter, NUMBER=?n], CASE=accusative] C[DE=?d, SECOND_POSITION=y]

# Genitive NPss as adverbs.
AdvP[DE=?d, DIRECTION=n, PREDICATE=n, PROS=n, REL=n, WH=?w] -> NP[ADVERB=y, CASE=genitive, DE=?d, FUSED_REL=n, PAS=n, PROPER=n, REL=n, WH=?w]

# Nominative adjectives as adverbs.
AdvP[AGR=?agr, DE=?d, DIRECTION=?dir, PREDICATE=n, PROS=n, REL=n, WH=n] -> A[AGR=?agr, ADVERB=y, CASE=nominative, STANDALONE=n] C[DE=?d, SECOND_POSITION=y]

# PPs as adverbs.
AdvP[DE=?d, DIRECTION=?dir, PREDICATE=y, PROS=?pros, REL=?r, WH=?w] -> PP[DE=?d, DIRECTION=?dir, PROS=?pros, REL=?r, WH=?w]

# Participles as adverbs.  Nominative participles agree with the subject of the VP; genitive participles don't have to.
AdvP[AGR=?agr, DE=?d, DIRECTION=n, PREDICATE=n, PROS=n, REL=n, WH=n] -> VP[AGR=?agr, CASE=nominative, DE=?d, MOOD=participle, VERBLESS=n]
AdvP[DE=?d, DIRECTION=n, PREDICATE=n, PROS=n, REL=n, WH=n] -> VP[AGR=?agr, CASE=genitive, DE=?d, MOOD=participle, VERBLESS=n]

# Genitive participles as adverbs, with genitive NPs as subjects.
AdvP[DE=?d, DIRECTION=n, PREDICATE=n, PROS=n, REL=n, WH=n] -> VP[AGR=?agr, CASE=genitive, DE=?d, MOOD=participle, VERBLESS=n] NP[CASE=genitive, DE=n]

#### Adding things to adverbs ####

AdvP[DE=?d, DIRECTION=?dir, PREDICATE=?p, PROS=?pros, REL=n, WH=?w] -> 'kai/_B-------' AdvP[DE=?d, DIRECTION=?dir, PREDICATE=?p, PROS=?pros, REL=n, WH=?w]

#### Adjuncts ####

# Vocatives.
AdjP[COMMA_OPTIONAL=n, DE=?d, WH=n] -> NP[CASE=vocative, DE=?d, FUSED_REL=n, WH=n]

# Particles.
AdjP[COMMA_OPTIONAL=y, DE=?d, WH=n] -> L[ADJUNCT=y, NEGATIVE=n] C[DE=?d, SECOND_POSITION=y]

#######################
#### Miscellaneous ####
#######################

#### Negation particles ####

# Negative adverbs (?).
Neg -> B[NEGATIVE=y]

# Negative particles.
Neg -> L[NEGATIVE=y]

#### Other ####

# ti oti.
I[CASE=accusative, WH=y, AGR=[PERSON=3, NUMBER=singular, GENDER=neuter]] -> 'ti/_I----ASN' 'o(/ti_C-------'

